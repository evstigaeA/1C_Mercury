#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СписокОбъектовДляНастройкиПечати() Экспорт
	
	СписокОбъектов = Новый СписокЗначений;
	
	СписокОбъектов.Добавить(Метаданные.Документы.АннулированиеВСД.ПолноеИмя(), Метаданные.Документы.АннулированиеВСД.Представление());
	СписокОбъектов.Добавить(Метаданные.Документы.АннулированиеВСДГрупповое.ПолноеИмя(), Метаданные.Документы.АннулированиеВСДГрупповое.Представление());
	СписокОбъектов.Добавить(Метаданные.Документы.ВнесениеНомеровТранспортныхСредств.ПолноеИмя(), Метаданные.Документы.ВнесениеНомеровТранспортныхСредств.Представление());
	СписокОбъектов.Добавить(Метаданные.Документы.ВнесениеНомеровТранспортныхСредствГрупповое.ПолноеИмя(), Метаданные.Документы.ВнесениеНомеровТранспортныхСредствГрупповое.Представление());
	СписокОбъектов.Добавить(Метаданные.Документы.ВСД.ПолноеИмя(), Метаданные.Документы.ВСД.Представление());
	СписокОбъектов.Добавить(Метаданные.Документы.ПриходныеОперации.ПолноеИмя(), Метаданные.Документы.ПриходныеОперации.Представление());
	СписокОбъектов.Добавить(Метаданные.Документы.ПриходныеОперацииСводно.ПолноеИмя(), Метаданные.Документы.ПриходныеОперацииСводно.Представление());
	СписокОбъектов.Добавить(Метаданные.Документы.ТранспортныеОперации.ПолноеИмя(), Метаданные.Документы.ТранспортныеОперации.Представление());
	
	Возврат СписокОбъектов;
КонецФункции

Функция ВыриантыРазбивкиДокументовНаСтраницы() Экспорт
	
	СписокВариантов = Новый СписокЗначений;
	СписокВариантов.Добавить("ВсеДокументыНаОдномЛисте", "Все документы на одной странице");
	СписокВариантов.Добавить("КаждыйДокументОтдельно", "Каждый документ на отдельной странице");
	СписокВариантов.Добавить("Настраиваемый", "Настраиваемый");
	
	Возврат СписокВариантов;
КонецФункции

Функция КоллекцияРеквизитовДокументаДляРазбивкиСтраниц() Экспорт
	
	КоллекцияРеквизитов = Новый СписокЗначений;
	
	КоллекцияРеквизитов.Добавить("Организация"           , "Организация");
	КоллекцияРеквизитов.Добавить("КонтрагентОтправитель" , "Хоз. субъект отправитель");
	КоллекцияРеквизитов.Добавить("ПредприятиеОтправитель", "Предприятие отправитель");
	КоллекцияРеквизитов.Добавить("КонтрагентПолучатель"  , "Хоз. субъект получатель");
	КоллекцияРеквизитов.Добавить("ПредприятиеПолучатель" , "Предприятие получатель");
	КоллекцияРеквизитов.Добавить("ТипТТН"                , "Тип ТТН");
	КоллекцияРеквизитов.Добавить("НомерТТН"              , "Номер ТТН");
	КоллекцияРеквизитов.Добавить("ДатаТТН"               , "Дата ТТН");
	КоллекцияРеквизитов.Добавить("Транспорт"             , "Транспорт");
	
	Возврат КоллекцияРеквизитов;
КонецФункции

Функция ПараметрыПечатиОбъекта(ТипОбъекта, Пользователь = Неопределено) Экспорт
	
	НастройкиПечати = Новый Структура("ВариантРазбивкиДокументовНаСтраницы, Настройки");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиПечатиОбъектов.ВариантРазбивкиДокументовНаСтраницы КАК ВариантРазбивкиДокументовНаСтраницы,
		|	НастройкиПечатиОбъектов.Настройки КАК Настройки,
		|	1 КАК Приоритет
		|ПОМЕСТИТЬ ПараметрыПечати
		|ИЗ
		|	РегистрСведений.НастройкиПечатиОбъектов КАК НастройкиПечатиОбъектов
		|ГДЕ
		|	НастройкиПечатиОбъектов.ТипОбъекта = &ТипОбъекта
		|	И НастройкиПечатиОбъектов.Пользователь = &Пользователь
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиПечатиОбъектов.ВариантРазбивкиДокументовНаСтраницы,
		|	НастройкиПечатиОбъектов.Настройки,
		|	2
		|ИЗ
		|	РегистрСведений.НастройкиПечатиОбъектов КАК НастройкиПечатиОбъектов
		|ГДЕ
		|	НастройкиПечатиОбъектов.ТипОбъекта = &ТипОбъекта
		|	И НастройкиПечатиОбъектов.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(ПараметрыПечати.Приоритет) КАК Приоритет
		|ПОМЕСТИТЬ ТаблицаИтог
		|ИЗ
		|	ПараметрыПечати КАК ПараметрыПечати
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПараметрыПечати.ВариантРазбивкиДокументовНаСтраницы КАК ВариантРазбивкиДокументовНаСтраницы,
		|	ПараметрыПечати.Настройки КАК Настройки
		|ИЗ
		|	ТаблицаИтог КАК ТаблицаИтог
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыПечати КАК ПараметрыПечати
		|		ПО ТаблицаИтог.Приоритет = ПараметрыПечати.Приоритет
		|ГДЕ
		|	НЕ ТаблицаИтог.Приоритет ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ТипОбъекта"  , ТипОбъекта);
	Запрос.УстановитьПараметр("Пользователь", ?(ЗначениеЗаполнено(Пользователь), Пользователь, Справочники.Пользователи.ПустаяСсылка()));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		НастройкиПечати.ВариантРазбивкиДокументовНаСтраницы = "ВсеДокументыНаОдномЛисте";
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		НастройкиПечати.ВариантРазбивкиДокументовНаСтраницы = Выборка.ВариантРазбивкиДокументовНаСтраницы;
		НастройкиПечати.Настройки = Выборка.Настройки;
		
	КонецЕсли;
	
	Возврат НастройкиПечати;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура добавляет запись в регистр по переданным значениям структуры.
Процедура ДобавитьЗапись(СтруктураЗаписи, Загрузка = Ложь) Экспорт
	
	ОбменДаннымиСервер.ДобавитьЗаписьВРегистрСведений(СтруктураЗаписи, "НастройкиПечатиОбъектов", Загрузка);
	
КонецПроцедуры

// Процедура удаляет набор записей в регистре по переданным значениям структуры.
Процедура УдалитьЗапись(СтруктураЗаписи, Загрузка = Ложь) Экспорт
	
	ОбменДаннымиСервер.УдалитьНаборЗаписейВРегистреСведений(СтруктураЗаписи, "НастройкиПечатиОбъектов", Загрузка);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли