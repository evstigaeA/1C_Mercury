#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	СписокОбъектовДляНастройкиПечати = РегистрыСведений.НастройкиПечатиОбъектов.СписокОбъектовДляНастройкиПечати();
	
	Для Каждого ТекОбъект Из СписокОбъектовДляНастройкиПечати Цикл
		Элементы.ТипОбъекта.СписокВыбора.Добавить(ТекОбъект.Значение, ТекОбъект.Представление);
	КонецЦикла;
	
	ВыриантыРазбивкиДокументовНаСтраницы = РегистрыСведений.НастройкиПечатиОбъектов.ВыриантыРазбивкиДокументовНаСтраницы();
	
	Для Каждого ТекВариант Из ВыриантыРазбивкиДокументовНаСтраницы Цикл
		Элементы.ВариантРазбивкиДокументовНаСтраницы.СписокВыбора.Добавить(ТекВариант.Значение, ТекВариант.Представление);
	КонецЦикла;
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		
		КоллекцияРеквизитовДокументаДляРазбивкиСтраниц = РегистрыСведений.НастройкиПечатиОбъектов.КоллекцияРеквизитовДокументаДляРазбивкиСтраниц();
		
		Для Каждого ТекРеквизит Из КоллекцияРеквизитовДокументаДляРазбивкиСтраниц Цикл
			РеквизитыРазбивкиСтраниц.Добавить(ТекРеквизит.Значение, ТекРеквизит.Представление);
		КонецЦикла;
		
	Иначе
		
		ЗаписьОбъект = РеквизитФормыВЗначение("Запись");
		ЗаписьОбъект.Прочитать();
		СохраненноеЗначение = ЗаписьОбъект.Настройки.Получить();
		Для Каждого ТекЗначение Из СохраненноеЗначение Цикл
			ЗаполнитьЗначенияСвойств(РеквизитыРазбивкиСтраниц.Добавить(), ТекЗначение);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ПредставлениеТипаОбъекта = Элементы.ТипОбъекта.СписокВыбора.НайтиПоЗначению(ТекущийОбъект.ТипОбъекта);
	ТекущийОбъект.ПредставлениеВариантаРазбивкиДокументовНаСтраницы = Элементы.ВариантРазбивкиДокументовНаСтраницы.СписокВыбора.НайтиПоЗначению(ТекущийОбъект.ВариантРазбивкиДокументовНаСтраницы);
	ТекущийОбъект.Настройки = Новый ХранилищеЗначения(РеквизитыРазбивкиСтраниц);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантРазбивкиДокументовНаСтраницыПриИзменении(Элемент)
	
	РеквизитыРазбивкиСтраниц.ЗаполнитьПометки(Ложь);
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовСпискаФормы

&НаКлиенте
Процедура РеквизитыРазбивкиСтраницПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыРазбивкиСтраницПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДоступностьЭлементовФормы()
	
	Элементы.РеквизитыРазбивкиСтраниц.ТолькоПросмотр = Запись.ВариантРазбивкиДокументовНаСтраницы <> "Настраиваемый";
	
КонецПроцедуры

#КонецОбласти