#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиОбновления

// Заполняет регистр сведений НастройкиВеденияЖурналовЗагрузкиДанных.
//
Процедура ЗаполнитьНастройкиВеденияЖурналовЗагрузкиДанных() Экспорт
	
	МассивИменЖурналов = Новый Массив;
	МассивИменЖурналов.Добавить("РегистрСведений.ЖурналЗагрузкиДанныхПоВСД");
	МассивИменЖурналов.Добавить("РегистрСведений.ЖурналЗагрузкиДанныхПоЗаписямСкладскогоЖурнала");
	МассивИменЖурналов.Добавить("РегистрСведений.ЖурналВыполненияРегламентныхЗаданий");
	МассивИменЖурналов.Добавить("РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций");
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ИдентификаторыОбъектовМетаданных.Ссылка
	|ИЗ
	|	Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	|ГДЕ
	|	ИдентификаторыОбъектовМетаданных.ПолноеИмя В (&МассивИменЖурналов)");
	Запрос.УстановитьПараметр("МассивИменЖурналов", МассивИменЖурналов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.НастройкиВеденияЖурналовЗагрузкиДанных.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ВидОбъекта.Установить(Выборка.Ссылка);
		
		//Данные уже есть в регистре трогать не будем
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
	
		НоваяЗапись  = НаборЗаписей.Добавить();
		НоваяЗапись.ВидОбъекта = Выборка.Ссылка;
		НоваяЗапись.ВариантВеденияЖурнала       = Перечисления.ВариантыВеденияЖурналовЗагрузкиДанных.Полный;
		НоваяЗапись.ВариантОчисткиДанныхЖурнала = Перечисления.ВариантыОчисткиЖурналовЗагрузкиДанных.НеОчищать;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли