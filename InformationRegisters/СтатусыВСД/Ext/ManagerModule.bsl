#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает текущий статус документа
//
// Параметры:
//  СсылкаНаДокумент - ДокументСсылка - ссылка на документ типа:
//  									ДокументСсылка.ВСД.
// 
// Возвращаемое значение:
//  СтатусОбъекта - СправочникСсылка.СтатусыВСД, Неопределено - текущий статус документа.
//
Функция ТекущийСтатусДокумента(СсылкаНаДокумент) Экспорт 

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтатусыВСДСрезПоследних.СтатусВСД КАК Статус
	|ИЗ
	|	РегистрСведений.СтатусыВСД.СрезПоследних(, ВСД = &СсылкаНаДокумент) КАК СтатусыВСДСрезПоследних";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("СсылкаНаДокумент", СсылкаНаДокумент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли; 

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Статус;
	
КонецФункции // ()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура добавляет запись в регистр по переданным значениям структуры.
Процедура ДобавитьЗапись(СтруктураЗаписи, Загрузка = Ложь) Экспорт
	
	ОбменДаннымиСервер.ДобавитьЗаписьВРегистрСведений(СтруктураЗаписи, "СтатусыВСД", Загрузка);
	
КонецПроцедуры

// Процедура удаляет набор записей в регистре по переданным значениям структуры.
Процедура УдалитьЗапись(СтруктураЗаписи, Загрузка = Ложь) Экспорт
	
	ОбменДаннымиСервер.УдалитьНаборЗаписейВРегистреСведений(СтруктураЗаписи, "СтатусыВСД", Загрузка);
	
КонецПроцедуры

Функция СвойстваСтатусаВСД(ВСД, СтатусВСД) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтатусыВСД.Период КАК Период,
		|	СтатусыВСД.ВСД КАК ВСД,
		|	СтатусыВСД.СтатусВСД КАК СтатусВСД,
		|	СтатусыВСД.Ответственный КАК Ответственный,
		|	СтатусыВСД.Комментарий КАК Комментарий
		|ИЗ
		|	РегистрСведений.СтатусыВСД КАК СтатусыВСД
		|ГДЕ
		|	СтатусыВСД.ВСД = &ВСД
		|	И СтатусыВСД.СтатусВСД = &СтатусВСД";
	
	Запрос.УстановитьПараметр("ВСД", ВСД);
	Запрос.УстановитьПараметр("СтатусВСД", СтатусВСД);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	
	СтруктураСвойствСтатусаВСД = Новый Структура;
	СтруктураСвойствСтатусаВСД.Вставить("Период", ВыборкаДетальныеЗаписи.Период);
	СтруктураСвойствСтатусаВСД.Вставить("ВСД", ВыборкаДетальныеЗаписи.ВСД);
	СтруктураСвойствСтатусаВСД.Вставить("СтатусВСД", ВыборкаДетальныеЗаписи.СтатусВСД);
	СтруктураСвойствСтатусаВСД.Вставить("Ответственный", ВыборкаДетальныеЗаписи.Ответственный);
	СтруктураСвойствСтатусаВСД.Вставить("Комментарий", ВыборкаДетальныеЗаписи.Комментарий);
	
	Возврат СтруктураСвойствСтатусаВСД;
	
КонецФункции

#КонецОбласти

#КонецЕсли