#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.ТипРеквизита) Тогда
		
		УстановитьОграничениеТипаРеквизита();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.ИмяРеквизита) Тогда
		
		УстановитьОграничениеСпискаЗначение();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗначениеРеквизитаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Запись.ЗначениеРеквизита) и Не ЗначениеЗаполнено(Запись.ТипРеквизита) Тогда
		ПолучитьТипВыбанногоЗначения();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает доступные для выбора типы, реквизита ЗначениеРеквизита
// Доступные типы берутся реквизита ТипРеквизита записи регистра.
//
&НаСервере
Процедура УстановитьОграничениеТипаРеквизита()
	ТипРеквизита=Запись.ТипРеквизита;
	Элемент=Элементы.ЗначениеРеквизита;
	
	Если Найти(ТипРеквизита,"Ссылка.")=0 Тогда
		ТипРеквизита = СтрЗаменить(ТипРеквизита,".","Ссылка.");
	КонецЕсли;
	
	Тип = Новый ОписаниеТипов(ТипРеквизита);
	
	Элемент.ОграничениеТипа = Тип;
	Тип.ПривестиЗначение(Элемент);

КонецПроцедуры

// Устанавливает доступные для выбора значения, для некоторых реквизитов
&НаСервере
Процедура УстановитьОграничениеСпискаЗначение()
	
	Если Запись.ИмяРеквизита = "ТипТТН" Тогда
		Элементы.ЗначениеРеквизита.РежимВыбораИзСписка = Истина;
		Элементы.ЗначениеРеквизита.СписокВыбора.ЗагрузитьЗначения(Справочники.ТипыТТН.СписокРазрешенныхТиповТТН());
	ИначеЕсли Запись.ИмяРеквизита = "ФорматДатыВыработки" Или Запись.ИмяРеквизита = "ФорматСрокаГодности" Тогда
		Элементы.ЗначениеРеквизита.РежимВыбораИзСписка = Истина;
		Элементы.ЗначениеРеквизита.СписокВыбора.Добавить(Перечисления.СпособыЗаданияДаты.Дата);
		Элементы.ЗначениеРеквизита.СписокВыбора.Добавить(Перечисления.СпособыЗаданияДаты.Месяц);
		Элементы.ЗначениеРеквизита.СписокВыбора.Добавить(Перечисления.СпособыЗаданияДаты.Год);
		Если Запись.ИмяРеквизита = "ФорматДатыВыработки" Тогда
			Элементы.ЗначениеРеквизита.СписокВыбора.Добавить(Перечисления.СпособыЗаданияДаты.Неизвестно);
		Иначе
			Элементы.ЗначениеРеквизита.СписокВыбора.Добавить(Перечисления.СпособыЗаданияДаты.НеОграничен);
		КонецЕсли;
	ИначеЕсли Запись.ИмяРеквизита = "ПроводиласьВетсанэкспертиза" Тогда
		Элементы.ЗначениеРеквизита.РежимВыбораИзСписка = Истина;
		Элементы.ЗначениеРеквизита.СписокВыбора.ЗагрузитьЗначения(Справочники.РезультатыЛабораторногоИсследования.МассивРазрешенныхРезультатовИсследований());
	ИначеЕсли Запись.ИмяРеквизита = "ТипМероприятия" Тогда
		Элементы.ЗначениеРеквизита.РежимВыбораИзСписка = Истина;
		Элементы.ЗначениеРеквизита.СписокВыбора.Добавить(Справочники.ТипыМероприятий.ИммунизацияЖивогоЖивотного);
		Элементы.ЗначениеРеквизита.СписокВыбора.Добавить(Справочники.ТипыМероприятий.ОбработкаЖивогоЖивотного);
	ИначеЕсли Запись.ИмяРеквизита = "РезультатИсследования" Тогда
		Элементы.ЗначениеРеквизита.РежимВыбораИзСписка = Истина;
		Элементы.ЗначениеРеквизита.СписокВыбора.Добавить(Справочники.РезультатыЛабораторногоИсследования.ОтрицательныйРезультат);
		Элементы.ЗначениеРеквизита.СписокВыбора.Добавить(Справочники.РезультатыЛабораторногоИсследования.ПоложительныйРезультат);
		Элементы.ЗначениеРеквизита.СписокВыбора.Добавить(Справочники.РезультатыЛабораторногоИсследования.РезультатНевозможноОпределить);
		Если Запись.ВидОбъекта.ПолноеИмя = "Документ.ВнесениеСведенийОЛабораторныхИсследованиях" Тогда
			Элементы.ЗначениеРеквизита.СписокВыбора.Добавить(Справочники.РезультатыЛабораторногоИсследования.РезультатНеизвестен);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

 // Заполняет реквизит ТипРеквизита полным именем объекта метаданных ЗначенияРеквизита
 //
 // Параметры
 //  <Параметр1>  - <Тип.Вид> - <описание параметра>
 //                 <продолжение описания параметра>
 //  <Параметр2>  - <Тип.Вид> - <описание параметра>
 //                 <продолжение описания параметра>
 //
&НаСервере
Процедура ПолучитьТипВыбанногоЗначения()
	Если ТипЗнч(Запись.ЗначениеРеквизита) = Тип("Строка") Тогда
		ПолноеИмя = "Строка";
	ИначеЕсли ТипЗнч(Запись.ЗначениеРеквизита) = Тип("Число") Тогда
		ПолноеИмя = "Число";
	ИначеЕсли ТипЗнч(Запись.ЗначениеРеквизита) = Тип("Дата") Тогда
		ПолноеИмя = "Дата";
	ИначеЕсли ТипЗнч(Запись.ЗначениеРеквизита) = Тип("Булево") Тогда
		ПолноеИмя = "Булево";
	Иначе
		ПолноеИмя = Запись.ЗначениеРеквизита.Метаданные().ПолноеИмя();
	КонецЕсли;
	Запись.ТипРеквизита = ПолноеИмя;
КонецПроцедуры

#КонецОбласти