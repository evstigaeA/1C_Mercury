#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбновлениеИнформационнойБазы

// Монопольный обработчик обновления 2.0.5.1
Процедура ЗаполнитьДопустимыеФорматыДатПродукции() Экспорт
	
	Макет = ПолучитьМакет("Макет");
	Для ТекСтр=2 По Макет.ВысотаТаблицы Цикл
		
		КодТипаПродукции = Макет.Область("R"+ТекСтр+"C2").Текст;
		
		ТипПродукции = Справочники.ТипыПродукции.НайтиПоКоду(Число(КодТипаПродукции));
		
		ВидПродукции    = Справочники.ВидыПродукции.ПустаяСсылка();
		ПодвидПродукции = Справочники.ПодвидыПродукции.ПустаяСсылка();
		
		Если ТипПродукции <> Справочники.ТипыПродукции.ПустаяСсылка() Тогда
			
			GUIDВидаПродукции = Макет.Область("R"+ТекСтр+"C4").Текст;
			
			Если ЗначениеЗаполнено(GUIDВидаПродукции) Тогда
				
				СтруктураПоиска = Новый Структура("GUID_Меркурий", GUIDВидаПродукции);
				СтруктураЗаполнения = Новый Структура;
				СтруктураЗаполнения.Вставить("Владелец"     , ТипПродукции);
				СтруктураЗаполнения.Вставить("GUID_Меркурий", GUIDВидаПродукции);
				СтруктураЗаполнения.Вставить("Наименование" , Макет.Область("R"+ТекСтр+"C3").Текст);
				СтруктураЗаполнения.Вставить("КодТНВЭД"     , Макет.Область("R"+ТекСтр+"C5").Текст);
				
				ВидПродукции = ИнтеграцияВетисAPIСервер.НайтиСоздатьЗаполнитьЭлементСправочника("ВидыПродукции", СтруктураПоиска, СтруктураЗаполнения);
				
				Если ЗначениеЗаполнено(ВидПродукции) Тогда
					
					GUIDПодвидаПродукции = Макет.Область("R"+ТекСтр+"C7").Текст;
					
					Если ЗначениеЗаполнено(GUIDПодвидаПродукции) Тогда
						
						СтруктураПоиска = Новый Структура("GUID_Меркурий", GUIDПодвидаПродукции);
						СтруктураЗаполнения = Новый Структура;
						СтруктураЗаполнения.Вставить("Владелец"     , ВидПродукции);
						СтруктураЗаполнения.Вставить("GUID_Меркурий", GUIDПодвидаПродукции);
						СтруктураЗаполнения.Вставить("Наименование" , Макет.Область("R"+ТекСтр+"C6").Текст);
						СтруктураЗаполнения.Вставить("КодТНВЭД"     , Макет.Область("R"+ТекСтр+"C8").Текст);
						
						ПодвидПродукции = ИнтеграцияВетисAPIСервер.НайтиСоздатьЗаполнитьЭлементСправочника("ПодвидыПродукции", СтруктураПоиска, СтруктураЗаполнения);
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			НаборЗаписей = РегистрыСведений.ДопустимыеФорматыДатПродукции.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ТипПродукции.Установить(ТипПродукции);
			НаборЗаписей.Отбор.ВидПродукции.Установить(ВидПродукции);
			НаборЗаписей.Отбор.ПодвидПродукции.Установить(ПодвидПродукции);
			
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НовЗапись = НаборЗаписей.Добавить();
			НовЗапись.ТипПродукции                       = ТипПродукции;
			НовЗапись.ВидПродукции                       = ВидПродукции;
			НовЗапись.ПодвидПродукции                    = ПодвидПродукции;
			НовЗапись.РазрешенаДатаВыработки             = Макет.Область("R"+ТекСтр+"C9").Текст;
			НовЗапись.РазрешенМесяцВыработки             = Макет.Область("R"+ТекСтр+"C10").Текст;
			НовЗапись.РазрешенГодВыработки               = Макет.Область("R"+ТекСтр+"C11").Текст;
			НовЗапись.РазрешенаНеизвестнаяДатаВыработки  = Макет.Область("R"+ТекСтр+"C12").Текст;
			НовЗапись.РазрешенаДатаСрокаГодности         = Макет.Область("R"+ТекСтр+"C13").Текст;
			НовЗапись.РазрешенМесяцСрокаГодности         = Макет.Область("R"+ТекСтр+"C14").Текст;
			НовЗапись.РазрешенГодСрокаГодности           = Макет.Область("R"+ТекСтр+"C15").Текст;
			НовЗапись.РазрешенНеограниченныйСрокГодности = Макет.Область("R"+ТекСтр+"C16").Текст;
			НовЗапись.БезСрокаГодности                   = Макет.Область("R"+ТекСтр+"C17").Текст;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли