Функция ВозможностьОформленияУполномоченногоГашения(Организация, Контрагент) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИСТИНА КАК УполномоченноеГашениеДоступно
		|ИЗ
		|	РегистрСведений.УполномоченноеГашение КАК УполномоченноеГашение
		|ГДЕ
		|	УполномоченноеГашение.Организация = &Организация
		|	И УполномоченноеГашение.Контрагент = &Контрагент";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент" , Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

Функция КонтрагентыУполномоченногоГашения(Организация, Предприятие = Неопределено, Текст = "") Экспорт
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УполномоченноеГашение.Контрагент КАК Контрагент";
	
	Если ЗначениеЗаполнено(Предприятие) Тогда
		ТекстЗапроса = ТекстЗапроса + "
			|ПОМЕСТИТЬ ВТ_Контрагенты";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
		|ИЗ
		|	РегистрСведений.УполномоченноеГашение КАК УполномоченноеГашение
		|ГДЕ
		|	УполномоченноеГашение.Организация = &Организация";
	
	Если Текст <> "" Тогда
		
		ТекстЗапроса = ТекстЗапроса + " И УполномоченноеГашение.Контрагент.Наименование ПОДОБНО &Текст";
		
		Запрос.УстановитьПараметр("Текст", "%" + Текст + "%");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Предприятие) Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СвязиМеждуКонтрагентамиИПредприятиями.Контрагент КАК Контрагент
			|ИЗ
			|	РегистрСведений.СвязиМеждуКонтрагентамиИПредприятиями КАК СвязиМеждуКонтрагентамиИПредприятиями
			|ГДЕ
			|	СвязиМеждуКонтрагентамиИПредприятиями.Контрагент В
			|			(ВЫБРАТЬ
			|				ВТ_Контрагенты.Контрагент КАК Контрагент
			|			ИЗ
			|				ВТ_Контрагенты КАК ВТ_Контрагенты)
			|	И СвязиМеждуКонтрагентамиИПредприятиями.Предприятие = &Предприятие";
		
		Запрос.УстановитьПараметр("Предприятие", Предприятие);
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ДанныеВыбора = Новый СписокЗначений;
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеВыбора.Добавить(Выборка.Контрагент);
		
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции

Функция ПредприятияУполномоченногоГашения(Организация, Контрагент = Неопределено, Текст = "") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УполномоченноеГашение.Контрагент КАК Контрагент
		|ПОМЕСТИТЬ ВТ_Контрагенты
		|ИЗ
		|	РегистрСведений.УполномоченноеГашение КАК УполномоченноеГашение
		|ГДЕ
		|	УполномоченноеГашение.Организация = &Организация
		|	И (УполномоченноеГашение.Контрагент = &Контрагент
		|			ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СвязиМеждуКонтрагентамиИПредприятиями.Предприятие КАК Предприятие
		|ИЗ
		|	РегистрСведений.СвязиМеждуКонтрагентамиИПредприятиями КАК СвязиМеждуКонтрагентамиИПредприятиями
		|ГДЕ
		|	СвязиМеждуКонтрагентамиИПредприятиями.Контрагент В
		|			(ВЫБРАТЬ
		|				ВТ_Контрагенты.Контрагент КАК Контрагент
		|			ИЗ
		|				ВТ_Контрагенты КАК ВТ_Контрагенты)
		|	#УсловиеПоНаименованию
		|
		|СГРУППИРОВАТЬ ПО
		|	СвязиМеждуКонтрагентамиИПредприятиями.Предприятие";
	
	Если Текст = "" Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#УсловиеПоНаименованию", "");
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#УсловиеПоНаименованию", "И СвязиМеждуКонтрагентамиИПредприятиями.Предприятие.Наименование ПОДОБНО &Текст");
		Запрос.УстановитьПараметр("Текст", "%" + Текст + "%");
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Иначе
		Запрос.УстановитьПараметр("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	КонецЕсли;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеВыбора.Добавить(Выборка.Предприятие);
		
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции