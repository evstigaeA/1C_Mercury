#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура добавляет запись в регистр по переданным значениям структуры.
Процедура ДобавитьЗапись(СтруктураЗаписи, Загрузка = Ложь) Экспорт
	
	ОбменДаннымиСервер.ДобавитьЗаписьВРегистрСведений(СтруктураЗаписи, "СоответствиеХозяйствующихСубъектовКонтрагентамУчетнойСистемы", Загрузка);
	
КонецПроцедуры

// Процедура удаляет набор записей в регистре по переданным значениям структуры.
Процедура УдалитьЗапись(СтруктураЗаписи, Загрузка = Ложь) Экспорт
	
	ОбменДаннымиСервер.УдалитьНаборЗаписейВРегистреСведений(СтруктураЗаписи, "СоответствиеХозяйствующихСубъектовКонтрагентамУчетнойСистемы", Загрузка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ПеренестиКонтрагентаУчетнойСистемыИХозяйствующегоСубъекта() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоответствиеХозяйствующихСубъектовКонтрагентамУчетнойСистемы.УдалитьХозяйствующийСубъект КАК УдалитьХозяйствующийСубъект,
		|	СоответствиеХозяйствующихСубъектовКонтрагентамУчетнойСистемы.УдалитьКонтрагентУчетнойСистемы КАК УдалитьКонтрагентУчетнойСистемы
		|ИЗ
		|	РегистрСведений.СоответствиеХозяйствующихСубъектовКонтрагентамУчетнойСистемы КАК СоответствиеХозяйствующихСубъектовКонтрагентамУчетнойСистемы";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.СоответствиеХозяйствующихСубъектовКонтрагентамУчетнойСистемы.СоздатьНаборЗаписей();
		НаборЗаписей.Прочитать();
		
		Для Каждого ТекСтрока Из НаборЗаписей Цикл
			ТекСтрока.КонтрагентУчетнойСистемы = ТекСтрока.УдалитьКонтрагентУчетнойСистемы;
			ТекСтрока.ХозяйствующийСубъект     = ТекСтрока.УдалитьХозяйствующийСубъект;
		КонецЦикла;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		Исключение
			ШаблонСообщения	= НСтр("ru = 'Не выполнено обновление записей регистра сведений ""Соответствие хозяйствующих субъектов контрагентам учетной системы""
                                    |%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.СоответствиеХозяйствующихСубъектовКонтрагентамУчетнойСистемы,,
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли