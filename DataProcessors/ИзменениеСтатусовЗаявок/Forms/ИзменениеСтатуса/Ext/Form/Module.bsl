#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Документ = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Предусмотрено открытие обработки только из документов.'");
	КонецЕсли;
	
	Если Не ПроверкаВозможностиСменыСтатуса() Тогда
		ВызватьИсключение НСтр("ru='Изменение предусмотрено для следующих статусов:
								|Новая заявка,
								|Заявка отправлена,
								|Ошибка отправки ID,
								|Заявка обрабатывается,
								|Ошибка обработки ответа'");
	КонецЕсли;
	
	Если ТекущийСтатус = Справочники.СтатусыЗаявок.Новая Тогда
		Элементы.СтатусПослеИзменения.СписокВыбора.Добавить(Справочники.СтатусыЗаявок.ЗаявкаОтмененаАдминистратором);
	ИначеЕсли ТекущийСтатус = Справочники.СтатусыЗаявок.Отправлена Тогда
		Элементы.СтатусПослеИзменения.СписокВыбора.Добавить(Справочники.СтатусыЗаявок.ЗаявкаОтмененаАдминистратором);
		Элементы.СтатусПослеИзменения.СписокВыбора.Добавить(Справочники.СтатусыЗаявок.Новая);
	ИначеЕсли ТекущийСтатус = Справочники.СтатусыЗаявок.ОшибкаОтправкиID Тогда
		Элементы.СтатусПослеИзменения.СписокВыбора.Добавить(Справочники.СтатусыЗаявок.ЗаявкаОтмененаАдминистратором);
		Элементы.СтатусПослеИзменения.СписокВыбора.Добавить(Справочники.СтатусыЗаявок.Новая);
	ИначеЕсли ТекущийСтатус = Справочники.СтатусыЗаявок.Обрабатывается Тогда
		Элементы.СтатусПослеИзменения.СписокВыбора.Добавить(Справочники.СтатусыЗаявок.ЗаявкаОтмененаАдминистратором);
		Элементы.СтатусПослеИзменения.СписокВыбора.Добавить(Справочники.СтатусыЗаявок.Новая);
		Элементы.СтатусПослеИзменения.СписокВыбора.Добавить(Справочники.СтатусыЗаявок.IDПолучен);
	ИначеЕсли ТекущийСтатус = Справочники.СтатусыЗаявок.ОшибкаОбработкиОтвета Тогда
		Элементы.СтатусПослеИзменения.СписокВыбора.Добавить(Справочники.СтатусыЗаявок.ЗаявкаОтмененаАдминистратором);
		Элементы.СтатусПослеИзменения.СписокВыбора.Добавить(Справочники.СтатусыЗаявок.IDПолучен);
	КонецЕсли;
	
	СтатусПослеИзменения = Справочники.СтатусыЗаявок.ЗаявкаОтмененаАдминистратором;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаСервере
Процедура ВыполнитьИзменениеСтатусаЗаявкиНаСервере(ТекстОшибки)
	
	Если Не Обработки.ИзменениеСтатусовЗаявок.ПроверитьРаботуФоновыхЗаданий(Параметры.Документ.Метаданные().Имя, ТекстОшибки) Тогда
		Возврат;
	КонецЕсли;
	
	Обработки.ИзменениеСтатусовЗаявок.УстановитьТекущийСтатусЗаяки(Параметры.Документ, ТекущийСтатус, СтатусПослеИзменения, ИдентификаторЗаявки, ТекстОшибки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьИзменениеСтатусаЗаявки(Команда)
	
	ТекстОшибки = "";
	
	Если ПроверитьЗаполнение() Тогда
		
		ВыполнитьИзменениеСтатусаЗаявкиНаСервере(ТекстОшибки);
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			
		Иначе
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Изменение статуса заявки выполнено.'"));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПроверкаВозможностиСменыСтатуса()
	
	МассивРазрешенныхСтатусов = Обработки.ИзменениеСтатусовЗаявок.СтатусыРазрешенныеДляИзменения();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОперацийСрезПоследних.ДокументСсылка КАК ДокументСсылка,
		|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОперацийСрезПоследних.GUID_Меркурий КАК ИдентификаторЗаявки,
		|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОперацийСрезПоследних.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.СрезПоследних(, ДокументСсылка = &Документ) КАК ЖурналРегистрацииСостоянийЗаявокНаОформлениеОперацийСрезПоследних
		|ГДЕ
		|	ЖурналРегистрацииСостоянийЗаявокНаОформлениеОперацийСрезПоследних.Статус В(&МассивРазрешенныхСтатусов)";
	
	Запрос.УстановитьПараметр("Документ"                 , Параметры.Документ);
	Запрос.УстановитьПараметр("МассивРазрешенныхСтатусов", МассивРазрешенныхСтатусов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ИдентификаторЗаявки = Выборка.ИдентификаторЗаявки;
		ТекущийСтатус       = Выборка.Статус;
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

#КонецОбласти