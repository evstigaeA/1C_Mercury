&НаСервере
Процедура ПолучитьПредприятиеПоОрганизации(Организация,Контрагент = Неопределено,Предприятие = Неопределено,тзОП) Экспорт
	
	Контрагент = ОбщегоНазначенияУВСВызовСервера.КонтрагентПоОрганизации(Организация);
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Нет связи организации "  + Организация + " с контрагентом (хозяйствующим субъектом). Пропущена");
		Возврат;
	КонецЕсли;	
	
	Если ПустаяСтрока(Организация.ДМ_КодSAP) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("У организации "  + Организация + " пустой код SAP. Пропущена");
		Возврат;
	КонецЕсли;	
	
	Предприятие = Справочники.Предприятия.ПустаяСсылка();
	лЗапрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Предприятия.Ссылка КАК Предприятие
	|ИЗ
	|	Справочник.Предприятия КАК Предприятия
	|ГДЕ
	|	Предприятия.ДМ_НомерПланта = &ДМ_КодSAP");
	
	лЗапрос.УстановитьПараметр("ДМ_КодSAP", Организация.ДМ_КодSAP);
	лВыборка = лЗапрос.Выполнить().Выгрузить();
	Если лВыборка.Количество() > 1 Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("По коду планта " + Организация.ДМ_КодSAP + " у организации "  + Организация + " найдено несколько предприятий. Пропущена");
		Возврат;
	Иначе
		Для Каждого ТекСтр Из  лВыборка Цикл
			Предприятие = ТекСтр.Предприятие;
			Прервать;
		КонецЦикла;		
	КонецЕсли;	
	
	ЗапрК = Новый Запрос;
	ЗапрК.Текст = "ВЫБРАТЬ
	              |	СвязиМеждуКонтрагентамиИПредприятиями.GLN КАК GLN
	              |ИЗ
	              |	РегистрСведений.СвязиМеждуКонтрагентамиИПредприятиями КАК СвязиМеждуКонтрагентамиИПредприятиями
	              |ГДЕ
	              |	СвязиМеждуКонтрагентамиИПредприятиями.Контрагент = &Контрагент
	              |	И СвязиМеждуКонтрагентамиИПредприятиями.Предприятие = &Предприятие";
	
	ЗапрК.Параметры.Вставить("Контрагент",Контрагент);
	ЗапрК.Параметры.Вставить("Предприятие",Предприятие);
	
	ВыбЗапрК = ЗапрК.Выполнить().Выбрать();
	Если ВыбЗапрК.Следующий() Тогда
		НовтзОП = тзОП.Добавить();
		НовтзОП.Организация = Организация;
		НовтзОП.Контрагент = Контрагент;
		НовтзОП.Предприятие = Предприятие;
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("У контрагента " + Контрагент + " нет связи с предприятием "  + Предприятие + ". Пропущен");
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНазначенияНаСервере()
	
	Если Объект.ДатаДок = Дата(1,1,1) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Пустая дата создаваемых документов назначений");
		Возврат;
	КонецЕсли;	
	
	тзП= Объект.Пользователи.Выгрузить();
	тзП.Свернуть("Пользователь","");
	Если тзП.Количество() < Объект.Пользователи.Количество() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Дубли в списке пользователей");
		Возврат;
	КонецЕсли;	
	
	тзОП = Новый ТаблицаЗначений;
	тзОП.Колонки.Добавить("Организация",Новый ОписаниеТипов("СправочникСсылка.Организации"));
	тзОП.Колонки.Добавить("Контрагент",Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	тзОП.Колонки.Добавить("Предприятие",Новый ОписаниеТипов("СправочникСсылка.Предприятия"));
	
	
	Если Объект.Организации.Количество() = 0 Тогда
		ЗапрО = Новый Запрос;
		ЗапрО.Текст = "ВЫБРАТЬ
		              |	Организации.Ссылка КАК Организация
		              |ИЗ
		              |	Справочник.Организации КАК Организации
		              |ГДЕ
		              |	Организации.ПометкаУдаления = ЛОЖЬ";
		
		ВыбЗапрО = ЗапрО.Выполнить().Выбрать();
		Пока ВыбЗапрО.Следующий() Цикл
			Контрагент = Неопределено;
			Предприятие = Неопределено;
			ПолучитьПредприятиеПоОрганизации(ВыбЗапрО.Организация,Контрагент,Предприятие,тзОП)
		КонецЦикла;	
	Иначе
		тзОрг = Объект.Организации.Выгрузить();
		тзОрг.Свернуть("Организация","");
		Если тзОрг.Количество() < Объект.Организации.Количество() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Дубли в списке организаций");
			Возврат;
		КонецЕсли;	
			
		Для Каждого ТекСтр из тзОрг Цикл
			Контрагент = Неопределено;
			Предприятие = Неопределено;
			ПолучитьПредприятиеПоОрганизации(ТекСтр.Организация,Контрагент,Предприятие,тзОП)
		КонецЦикла;	
	КонецЕсли;
	
	Если тзОП.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Нет данных для создания документов");
		Возврат;
	КонецЕсли;	
	
	
	КвоПольз = 0;
	КвоДок = 0;
	ТекДата = Объект.ДатаДок;
	Для каждого ТекСтр из Объект.Пользователи Цикл
		ТекПольз = ТекСтр.Пользователь;
		КвоПольз = КвоПольз + 1;
		ЗапрП = Новый Запрос;
		ЗапрП.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПредприятияОбслуживаемыеПользователем.Организация КАК Организация,
		|	ПредприятияОбслуживаемыеПользователем.Предприятие КАК Предприятие
		|ИЗ
		|	РегистрСведений.ПредприятияОбслуживаемыеПользователем.СрезПоследних(&Период, Пользователь = &Пользователь) КАК ПредприятияОбслуживаемыеПользователем
	    |";
		
		ЗапрП.УстановитьПараметр("Период"      , Объект.ДатаДок);
		ЗапрП.Параметры.Вставить("Пользователь",ТекПольз.ПользовательСистемыМеркурий);
		тзП = ЗапрП.Выполнить().Выгрузить();
		Для Каждого ТектзОП из тзОП Цикл
			СтрОтб = Новый Структура;
			СтрОтб.Вставить("Организация",ТектзОП.Организация);
			СтрОтб.Вставить("Предприятие",ТектзОП.Предприятие);
			Если тзП.НайтиСтроки(СтрОтб).Количество() = 0  Тогда
				 НовДок = Документы.НазначенияПредприятийОтветственнымЛицам.СоздатьДокумент();
				 НовДок.Дата = ТекДата;
				 НовДок.УстановитьНовыйНомер();
				 НовДок.Ответственный = Пользователи.АвторизованныйПользователь();
		         НовДок.Организация = ТектзОП.Организация;
				 НовДок.Контрагент = ТектзОП.Контрагент;
				 НовДок.Пользователь = ТекПольз;
				 НовСтр = НовДок.Предприятия.Добавить();
				 НовСтр.Предприятие = ТектзОП.Предприятие;
				 НовСтр.Операция = Перечисления.УдалитьВидыОперацииНазначенияПредприятий.Назначение;
				 Попытка 
					 НовДок.Записать(РежимЗаписиДокумента.Проведение);
				 Исключение
				 КонецПопытки;	 
				 ТекДата = ТекДата + 1;
				 КвоДок = КвоДок + 1;
			КонецЕсли;
		КонецЦикла;	
	КонецЦикла;	
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("---------------------------------------------------------");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для " + КвоПольз + " пользователей " + " создано " + КвоДок + " документов назначения предприятий");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНазначения(Команда)
	
	
	Если Объект.Пользователи.Количество() = 0  Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указано ни одного пользователя");
		Возврат;
	КонецЕсли;	
		
	ЗаполнитьНазначенияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.ДатаДок = ТекущаяДатаСеанса();
	
КонецПроцедуры
