
&НаКлиенте
Процедура Подбор(Команда)
	
	лСтруктура = Новый Структура("РежимВыбора,ЗакрыватьПриВыборе",Истина,Ложь);
	лФорма = ПолучитьФорму("Документ.ТранспортныеОперации.ФормаВыбора",лСтруктура,Элементы.ТаблицаОпераций);
	лФорма.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОперацийОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ТранспортныеОперации") Тогда
		лСтроки = ТаблицаОпераций.НайтиСтроки(Новый Структура("Документ",ВыбранноеЗначение));
		Если лСтроки.Количество() > 0 Тогда //не добавляем уже выбранные значения
			Возврат;
		КонецЕсли;
		лСтрока = ТаблицаОпераций.Добавить();
		лСтрока.Документ = ВыбранноеЗначение;
		лСтрока.Выбран = Истина;
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Для Каждого лЗначение Из ВыбранноеЗначение Цикл
			Если ТипЗнч(лЗначение) = Тип("ДокументСсылка.ТранспортныеОперации") Тогда
				лСтроки = ТаблицаОпераций.НайтиСтроки(Новый Структура("Документ",лЗначение));
				Если лСтроки.Количество() > 0 Тогда //не добавляем уже выбранные значения
					Продолжить;
				КонецЕсли;
				лСтрока = ТаблицаОпераций.Добавить();
				лСтрока.Документ = лЗначение;
				лСтрока.Выбран = Истина;
			КонецЕсли;
		КонецЦикла;
	Иначе	
		//Ничего не делаем
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтрОтветаПользователь = ДМ_СлужебныйПривилегированный.МассивПредприятийТекущегоПользователя();
	СпПред.ЗагрузитьЗначения(СтрОтветаПользователь.мПред);
	Если СпПред.Количество() = 1 Тогда
		НомерПланта = СпПред.Получить(0).Значение.ДМ_НомерПланта;
	КонецЕсли;	
		
	ДоступныВсеПредприятия = СтрОтветаПользователь.ДоступныВсеПредприятия;
	
	Если Параметры.Свойство("МассивОтобранныхДокументов") Тогда
		лМассив = Параметры.МассивОтобранныхДокументов;
		Если ТипЗнч(лМассив) = Тип("Массив") Тогда
			ЗапрПров = Новый Запрос;
			ЗапрПров.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			                 |	ТранспортныеОперации.Ссылка КАК Документ
			                 |ИЗ
			                 |	Документ.ТранспортныеОперации КАК ТранспортныеОперации
			                 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТранспортныеОперации.ТаблицаВСД КАК ТранспортныеОперацииТаблицаВСД
			                 |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыВСД.СрезПоследних КАК СтатусыВСДСрезПоследних
			                 |			ПО ТранспортныеОперацииТаблицаВСД.ВСД = СтатусыВСДСрезПоследних.ВСД
			                 |				И (СтатусыВСДСрезПоследних.СтатусВСД = ЗНАЧЕНИЕ(Справочник.СтатусыВСД.Оформлен))
			                 |		ПО ТранспортныеОперации.Ссылка = ТранспортныеОперацииТаблицаВСД.Ссылка
			                 |ГДЕ
			                 |	ТранспортныеОперации.Ссылка В(&МассивТО)";
			
			ЗапрПров.Параметры.Вставить("МассивТО",лМассив);
			лМассив2 = ЗапрПров.Выполнить().Выгрузить().ВыгрузитьКолонку("Документ");
			
			Для Каждого лЗначение Из лМассив2 Цикл
				лСтрока = ТаблицаОпераций.Добавить();
				лСтрока.Документ = лЗначение;
				лСтрока.Выбран = лЗначение.ПредприятиеПолучатель.ДМ_ПечатьТВСД;
			КонецЦикла;
			
			ПечатьИзОбщегоЖурнала = Истина;
			Возврат;
	
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("СписокДокументов") Тогда
		лМассив = Параметры.СписокДокументов;
		Если ТипЗнч(лМассив) = Тип("Массив") Тогда
			Для Каждого лЗначение Из лМассив Цикл
				лСтрока = ТаблицаОпераций.Добавить();
				лСтрока.Документ = лЗначение;
				лСтрока.Выбран = Истина;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ИзъятьИзТаблицыОперацийБезВСД();
	ПолучитьНеВыбранныеДокументы();
	
КонецПроцедуры

&НаСервере
Процедура ИзъятьИзТаблицыОперацийБезВСД()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОпераций.Документ КАК Ссылка
	|ПОМЕСТИТЬ втТЗ
	|ИЗ
	|	&ТаблицаОпераций КАК ТаблицаОпераций
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТранспортныеОперации.Ссылка КАК Ссылка,
	|	ТранспортныеОперации.НомерТТН КАК НомерТТН,
	|	ТранспортныеОперации.Номер КАК Номер
	|ПОМЕСТИТЬ ДокументыТранспортныеОперации
	|ИЗ
	|	втТЗ КАК втТЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортныеОперации КАК ТранспортныеОперации
	|		ПО ТранспортныеОперации.Ссылка = втТЗ.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТранспортныеОперации.Ссылка КАК Документ,
	|	КОЛИЧЕСТВО(ТранспортныеОперацииТаблицаВСД.Ссылка) КАК КоличествоВСД,
	|	КОЛИЧЕСТВО(ТранспортныеОперацииТаблицаПродукция.Ссылка) КАК КоличествоПродукции,
	|	ТранспортныеОперации.НомерТТН КАК НомерТТН,
	|	ТранспортныеОперации.Номер КАК Номер
	|ПОМЕСТИТЬ ДокументыТО
	|ИЗ
	|	ДокументыТранспортныеОперации КАК ТранспортныеОперации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортныеОперации.ТаблицаВСД КАК ТранспортныеОперацииТаблицаВСД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыВСД.СрезПоследних КАК СтатусыВСДСрезПоследних
	|			ПО ТранспортныеОперацииТаблицаВСД.ВСД = СтатусыВСДСрезПоследних.ВСД
	|				И (СтатусыВСДСрезПоследних.СтатусВСД = ЗНАЧЕНИЕ(Справочник.СтатусыВСД.Оформлен))
	|		ПО ТранспортныеОперации.Ссылка = ТранспортныеОперацииТаблицаВСД.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортныеОперации.ТаблицаПродукция КАК ТранспортныеОперацииТаблицаПродукция
	|		ПО (ТранспортныеОперацииТаблицаПродукция.Ссылка = ТранспортныеОперации.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТранспортныеОперации.Ссылка,
	|	ТранспортныеОперации.НомерТТН,
	|	ТранспортныеОперации.Номер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыТО.Документ КАК Документ,
	|	ИСТИНА КАК Выбран
	|ИЗ
	|	ДокументыТО КАК ДокументыТО
	|ГДЕ
	|	ДокументыТО.КоличествоВСД = ДокументыТО.КоличествоПродукции
	|	И ДокументыТО.КоличествоВСД > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыТО.НомерТТН,
	|	ДокументыТО.Номер";
    Запрос.УстановитьПараметр( "ТаблицаОпераций", ТаблицаОпераций.Выгрузить() );
	
	лРезультат = Запрос.Выполнить();
	ТаблицаОпераций.Очистить();
	ТаблицаОпераций.Загрузить( лРезультат.Выгрузить() );
	
КонецПроцедуры

&НаКлиенте
Процедура Напечатать(Команда)
	
	лМассив = Новый Массив;
	Для Каждого лСтрока Из ТаблицаОпераций Цикл
		Если лСтрока.Выбран Тогда
			лМассив.Добавить(лСтрока.Документ);
		КонецЕсли;
	КонецЦикла;
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.ТранспортныеОперации", "Сжатое_V2",лМассив, ЭтаФорма);	
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьНеВыбранныеДокументы()
	
	Массив = ТаблицаОпераций.Выгрузить().ВыгрузитьКолонку("Документ");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТранспортныеОперации.НомерТТН КАК НомерТТН,
	|	ТранспортныеОперации.ДМ_ДопНомерДокументаОтгрузки КАК ДМ_ДопНомерДокументаОтгрузки,
	|	ТранспортныеОперации.Транспорт КАК Транспорт
	|ПОМЕСТИТЬ Врт
	|ИЗ
	|	Документ.ТранспортныеОперации КАК ТранспортныеОперации
	|ГДЕ
	|	ТранспортныеОперации.Ссылка В(&Массив)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТранспортныеОперации.ДМ_ДопНомерДокументаОтгрузки,
	|	ТранспортныеОперации.НомерТТН,
	|	ТранспортныеОперации.Транспорт
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерТТН,
	|	ДМ_ДопНомерДокументаОтгрузки,
	|	Транспорт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТранспортныеОперации.Ссылка КАК Ссылка,
	|	ТранспортныеОперации.НомерТТН КАК НомерТТН,
	|	ТранспортныеОперации.Номер КАК Номер
	|ПОМЕСТИТЬ ДокументыТранспортныеОперации
	|ИЗ
	|	Врт КАК Врт
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортныеОперации КАК ТранспортныеОперации
	|		ПО Врт.НомерТТН = ТранспортныеОперации.НомерТТН
	|			И Врт.ДМ_ДопНомерДокументаОтгрузки = ТранспортныеОперации.ДМ_ДопНомерДокументаОтгрузки
	|			И Врт.Транспорт = ТранспортныеОперации.Транспорт
	|ГДЕ
	|	НЕ ТранспортныеОперации.Ссылка В (&Массив)
	|	И НЕ ТранспортныеОперации.Ссылка ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТранспортныеОперации.Ссылка КАК Документ,
	|	КОЛИЧЕСТВО(ТранспортныеОперацииТаблицаВСД.Ссылка) КАК КоличествоВСД,
	|	КОЛИЧЕСТВО(ТранспортныеОперацииТаблицаПродукция.Ссылка) КАК КоличествоПродукции,
	|	ТранспортныеОперации.НомерТТН КАК НомерТТН,
	|	ТранспортныеОперации.Номер КАК Номер
	|ПОМЕСТИТЬ ДокументыТО
	|ИЗ
	|	ДокументыТранспортныеОперации КАК ТранспортныеОперации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортныеОперации.ТаблицаВСД КАК ТранспортныеОперацииТаблицаВСД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыВСД.СрезПоследних КАК СтатусыВСДСрезПоследних
	|			ПО ТранспортныеОперацииТаблицаВСД.ВСД = СтатусыВСДСрезПоследних.ВСД
	|				И (СтатусыВСДСрезПоследних.СтатусВСД = ЗНАЧЕНИЕ(Справочник.СтатусыВСД.Оформлен))
	|		ПО ТранспортныеОперации.Ссылка = ТранспортныеОперацииТаблицаВСД.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортныеОперации.ТаблицаПродукция КАК ТранспортныеОперацииТаблицаПродукция
	|		ПО (ТранспортныеОперацииТаблицаПродукция.Ссылка = ТранспортныеОперации.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТранспортныеОперации.Ссылка,
	|	ТранспортныеОперации.НомерТТН,
	|	ТранспортныеОперации.Номер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыТО.Документ КАК Документ,
	|	ИСТИНА КАК Выбран
	|ИЗ
	|	ДокументыТО КАК ДокументыТО
	|ГДЕ
	|	ДокументыТО.КоличествоВСД = ДокументыТО.КоличествоПродукции
	|	И ДокументыТО.КоличествоВСД > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыТО.НомерТТН,
	|	ДокументыТО.Номер";
	
	Запрос.УстановитьПараметр("Массив", Массив);
	
	ТаблицаОперацийНеВыбранные.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ПечатьИзОбщегоЖурнала Тогда
		Объект.НомерПланта = "";
		Объект.НомерОтгрузки = "";
		Элементы.Подбор.Доступность = ДоступныВсеПредприятия;
	Иначе
		Если Не ДоступныВсеПредприятия и СпПред.Количество() = 0 Тогда
			Элементы.Подбор.Доступность = Ложь;
		ИначеЕсли Не ДоступныВсеПредприятия Тогда
			Если спПред.Количество() = 1 Тогда
				Объект.НомерПланта = НомерПланта;
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;	
		
	Если ТаблицаОперацийНеВыбранные.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("СпроситьОДобавленииЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "Выбраны не все документы с указанными номерами ТТН и номерами отгрузки. Добавить транспортные операции автоматически?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СпроситьОДобавленииЗавершение(Ответ, УзелИнформационнойБазы) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Для Каждого лСтрока Из ТаблицаОперацийНеВыбранные Цикл
			лНоваяСтрока = ТаблицаОпераций.Добавить();
			лНоваяСтрока.Документ = лСтрока.Документ;
			лНоваяСтрока.Выбран = Истина;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТранспортныеОперации.Ссылка КАК Ссылка,
	|	ТранспортныеОперации.НомерТТН КАК НомерТТН,
	|	ТранспортныеОперации.Номер КАК Номер,
	|	ТранспортныеОперации.ДМ_ДопНомерДокументаОтгрузки КАК НомерОтгрузки
	|ПОМЕСТИТЬ ДокументыТранспортныеОперации
	|ИЗ
	|	Документ.ТранспортныеОперации КАК ТранспортныеОперации
	|ГДЕ
	|	ТранспортныеОперации.ПредприятиеОтправитель.ДМ_НомерПланта = &НомерПланта
	|	И (ТранспортныеОперации.ПредприятиеОтправитель В (&мПред)
	|			ИЛИ &ДоступныВсеПредприятия = ИСТИНА)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТранспортныеОперации.Ссылка КАК Документ,
	|	КОЛИЧЕСТВО(ТранспортныеОперацииТаблицаВСД.Ссылка) КАК КоличествоВСД,
	|	КОЛИЧЕСТВО(ТранспортныеОперацииТаблицаПродукция.Ссылка) КАК КоличествоПродукции,
	|	ТранспортныеОперации.НомерТТН КАК НомерТТН,
	|	ТранспортныеОперации.Номер КАК Номер,
	|	ТранспортныеОперации.НомерОтгрузки КАК НомерОтгрузки
	|ПОМЕСТИТЬ ДокументыТО
	|ИЗ
	|	ДокументыТранспортныеОперации КАК ТранспортныеОперации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортныеОперации.ТаблицаВСД КАК ТранспортныеОперацииТаблицаВСД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыВСД.СрезПоследних КАК СтатусыВСДСрезПоследних
	|			ПО ТранспортныеОперацииТаблицаВСД.ВСД = СтатусыВСДСрезПоследних.ВСД
	|				И (СтатусыВСДСрезПоследних.СтатусВСД = ЗНАЧЕНИЕ(Справочник.СтатусыВСД.Оформлен))
	|		ПО ТранспортныеОперации.Ссылка = ТранспортныеОперацииТаблицаВСД.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортныеОперации.ТаблицаПродукция КАК ТранспортныеОперацииТаблицаПродукция
	|		ПО (ТранспортныеОперацииТаблицаПродукция.Ссылка = ТранспортныеОперации.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТранспортныеОперации.Ссылка,
	|	ТранспортныеОперации.НомерТТН,
	|	ТранспортныеОперации.Номер,
	|	ТранспортныеОперации.НомерОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыТО.Документ КАК Документ,
	|	ИСТИНА КАК Выбран,
	|	ДокументыТО.НомерОтгрузки КАК НомерОтгрузки
	|ИЗ
	|	ДокументыТО КАК ДокументыТО
	|ГДЕ
	|	ДокументыТО.КоличествоВСД = ДокументыТО.КоличествоПродукции
	|	И ДокументыТО.КоличествоВСД > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыТО.НомерТТН,
	|	ДокументыТО.Номер,
	|	ДокументыТО.НомерОтгрузки";
	
	
	Запрос.УстановитьПараметр( "НомерПланта", Объект.НомерПланта );
	Запрос.УстановитьПараметр( "мПред", спПред.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр( "ДоступныВсеПредприятия", ДоступныВсеПредприятия);
	
	
	лтзРезультат = Запрос.Выполнить().Выгрузить();
	лтзРезультат2 = лтзРезультат.Скопировать();
	лтзРезультат2.Очистить();
	
	СтрНО = СокрЛП(Объект.НомерОтгрузки);
	Пока Лев(СтрНО,1)= "0" Цикл
		СтрНО = Сред(СтрНО,2);
	КонецЦикла;
		
	Для Каждого ТекСтр из лтзРезультат Цикл
		СтрНО2 = СокрЛП(ТекСтр.НомерОтгрузки);
		Пока Лев(СтрНО2,1)= "0" Цикл
			СтрНО2 = Сред(СтрНО2,2);
		КонецЦикла;
		Если СтрНО = СтрНО2 Тогда
			НовСтр = лтзРезультат2.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр,ТекСтр);
		КонецЕсли;
	КонецЦикла;	
	
	Если лтзРезультат2.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю( НСтр("ru = 'Нет данных для заполнения'") );
		Возврат;
	КонецЕсли;
	
	ТаблицаОпераций.Загрузить( лтзРезультат2 );
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	ЭтотОбъект.ТаблицаОпераций.Очистить();
	ЗаполнитьНаСервере();
	Если ЭтотОбъект.ТаблицаОпераций.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю( НСтр("ru = 'Заполнение выполнено'") );
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	Для каждого лСтрока Из ТаблицаОпераций Цикл
		лСтрока.Выбран = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	Для каждого лСтрока Из ТаблицаОпераций Цикл
		лСтрока.Выбран = Ложь;
	КонецЦикла;
	
КонецПроцедуры
