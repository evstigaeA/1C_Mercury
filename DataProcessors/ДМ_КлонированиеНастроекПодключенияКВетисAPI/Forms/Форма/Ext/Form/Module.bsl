
&НаСервере
Процедура КлонироватьНастройкиНаСервере()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	лСписокПолей = "";
	Для каждого лРеквизит Из Метаданные.Справочники.НастройкиПодключенияКВетисAPI.Реквизиты Цикл
		лСписокПолей = лСписокПолей + "," + лРеквизит.Имя;
	КонецЦикла;
	лСписокПолей = Сред( лСписокПолей, 2 );
	
	Для каждого лСтрокаТЧ Из Объект.Организации Цикл
		
		Если Не лСтрокаТЧ.Обновлять Тогда
			Продолжить;
		КонецЕсли;
		
		лЭлемент = Справочники.НастройкиПодключенияКВетисAPI.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств( лЭлемент, Объект.НастройкиПодключенияКВетисAPI, лСписокПолей );
		лЭлемент.Организация = лСтрокаТЧ.Организация;
		лЭлемент.Наименование = лСтрокаТЧ.Наименование;
		лЭлемент.УстановитьНовыйКод();
		лЭлемент.Записать();
		
		лСтрокаТЧ.Обновлять = Ложь;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КлонироватьНастройки(Команда)
	КлонироватьНастройкиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуНаСервере()
	
	Объект.Организации.Очистить();
	лЗапрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	Организации.ДМ_КодSAP КАК ДМ_КодSAP,
	|	Организации.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиПодключенияКВетисAPI КАК НастройкиПодключенияКВетисAPI
	|		ПО НастройкиПодключенияКВетисAPI.Организация = Организации.Ссылка
	|ГДЕ
	|	Организации.ПометкаУдаления = ЛОЖЬ
	|	И Организации.ДМ_КодSAP > ""0""
	|	И НастройкиПодключенияКВетисAPI.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование"
	);
	лПустаяОрганизация = Справочники.Организации.ПустаяСсылка();
	лРезультат = лЗапрос.Выполнить();
	Если лРезультат.Пустой() Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю( НСтр("ru = 'Нет организаций с кодом SAP'") );
		Возврат;
	
	КонецЕсли;
	
	лВыборка = лРезультат.Выбрать();
	Пока лВыборка.Следующий() Цикл
		
		лНоваяСтрока = Неопределено;
		
		Если Не ПустаяСтрока(лВыборка.ДМ_КодSAP) Тогда
			лНоваяСтрока = Объект.Организации.Добавить();
			лНоваяСтрока.Обновлять = Истина;
			лНоваяСтрока.Организация = лВыборка.Ссылка;
			лНоваяСтрока.Наименование = лВыборка.Наименование;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Объект.Организации.Количество() = 0 Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю( НСтр("ru = 'Нет подходящих данных для заполнения'") );
		Возврат;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицу(Команда)
	ЗаполнитьТаблицуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	Для каждого лСтрока Из Объект.Организации Цикл
		лСтрока.Обновлять = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	Для каждого лСтрока Из Объект.Организации Цикл
		лСтрока.Обновлять = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОрганизацииОрганизацияПриИзмененииНаСервере( пОрганизация )
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта( пОрганизация, "Наименование", Истина );
КонецФункции

&НаКлиенте
Процедура ОрганизацииОрганизацияПриИзменении(Элемент)
	
	Элементы.Организации.ТекущиеДанные.Наименование = ОрганизацииОрганизацияПриИзмененииНаСервере(Элементы.Организации.ТекущиеДанные.Организация);
	
КонецПроцедуры
