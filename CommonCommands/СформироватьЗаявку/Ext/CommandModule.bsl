&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("УправляемаяФорма") Тогда
		
		Если ПараметрыВыполненияКоманды.Источник.Параметры.Свойство("Ключ")
			И ЗначениеЗаполнено(ПараметрыВыполненияКоманды.Источник.Параметры.Ключ) Тогда
			
			Если ПараметрыВыполненияКоманды.Источник.Объект.Проведен Тогда
				
				Если ПараметрыВыполненияКоманды.Источник.ПроверитьЗаполнение() Тогда
					ОбработкаКомандыПродолжение(ПараметрКоманды, Ложь);
				КонецЕсли;
				
			Иначе
				
				Если ПараметрыВыполненияКоманды.Источник.Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение)) Тогда
					ОбработкаКомандыПродолжение(ПараметрКоманды, Ложь);
				КонецЕсли;
				
			КонецЕсли;
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	МассивДокументов = ПараметрКоманды;
	
	Если ЕстьНепроведенные(МассивДокументов) Тогда 
		Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаНаВопрос", ЭтотОбъект, МассивДокументов);
		ТекстВопроса = НСтр("ru = 'Перед формированием заявки документы должны быть проведены. Выполнить проведение?';");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ОбработкаКомандыПродолжение(МассивДокументов)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаКомандыПродолжение(ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыПродолжение(МассивДокументов, ВызовИзФормыСписка = Истина)
	
	ОбработкаКомандыНаСервере(МассивДокументов, ВызовИзФормыСписка);
	Оповестить("ИзменениеСтатусаЗаявкиНаОформлениеОперации", МассивДокументов);
	
КонецПроцедуры

&НаСервере
Функция ЕстьНепроведенные(МассивДокументов)
	
	Для Каждого Документ Из МассивДокументов Цикл
		Если Не Документ.Проведен Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

&НаСервере
Процедура ОбработкаКомандыНаСервере(МассивДокументов, ВызовИзФормыСписка)
	
	ИнтеграцияВетисAPIОтправкаЗаявокНаОформление.СформироватьЗаявки(МассивДокументов,, Ложь, ВызовИзФормыСписка);
	
КонецПроцедуры