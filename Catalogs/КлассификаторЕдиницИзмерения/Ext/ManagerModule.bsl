#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращает единицу измерения по умолчанию "шт".
// 
// Возвращаемое значение:
//  СправочникСсылка - ссылка на объект справочника КлассификаторЕдиницИзмерения.
//
Функция ПолучитьЕдиницуИзмеренияПоУмолчанию() Экспорт
	
	ЕдиницаПоУмолчанию = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("796");	
	Если ЕдиницаПоУмолчанию = Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка() Тогда
		ЕдиницаПоУмолчанию = Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию("шт", Истина);
	КонецЕсли;
	
	Возврат ЕдиницаПоУмолчанию;
	
КонецФункции

// Заполнение справочника КлассификаторЕдиницИзмерения на основании макета справочника
//
Процедура ЗагрузитьЭлементы() Экспорт
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	Макет = Справочники.КлассификаторЕдиницИзмерения.ПолучитьМакет("КлассификаторЕдиницИзмерения");
	Макет.Записать(ИмяВременногоФайла);
	Дерево = ЗначениеИзФайла(ИмяВременногоФайла);
	
	Соответствие = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕдиницыИзмерения.Ссылка КАК Ссылка,
	|	ЕдиницыИзмерения.Код КАК Код
	|ИЗ
	|	Справочник.КлассификаторЕдиницИзмерения КАК ЕдиницыИзмерения";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Соответствие.Вставить(Выборка.Код, Выборка.Код);
	КонецЦикла;
	Н = 1;
	НачатьТранзакцию();
	Для каждого СтрокаУровень1 Из Дерево.Строки Цикл
		Если Н = 6 Тогда
			Прервать;
		КонецЕсли;
		Для каждого СтрокаУровень2 Из СтрокаУровень1.Строки Цикл
			Если Н = 6 Тогда
				Прервать;
			КонецЕсли;
			Для каждого СтрокаУровень3 Из СтрокаУровень2.Строки Цикл
				
				Если Соответствие.Получить(СтрокаУровень3.КодЧисловой) = Неопределено Тогда
					 СправочникОбъект = Справочники.КлассификаторЕдиницИзмерения.СоздатьЭлемент();
                    Если ЗначениеЗаполнено(СтрокаУровень3.УсловноеОбозначениеНациональное) Тогда
						Наименование = СтрокаУровень3.УсловноеОбозначениеНациональное;
					ИначеЕсли ЗначениеЗаполнено(СтрокаУровень3.УсловноеОбозначениеМеждународное) Тогда
						Наименование = СтрокаУровень3.УсловноеОбозначениеМеждународное;
					ИначеЕсли ЗначениеЗаполнено(СтрокаУровень3.КодовоеБуквенноеОбозначениеНациональное) Тогда
						Наименование = СтрокаУровень3.КодовоеБуквенноеОбозначениеНациональное;
					ИначеЕсли ЗначениеЗаполнено(СтрокаУровень3.КодовоеБуквенноеОбозначениеМеждународное) Тогда
						Наименование = СтрокаУровень3.КодовоеБуквенноеОбозначениеМеждународное;
					Иначе
						Наименование = СтрокаУровень3.Наименование;
					КонецЕсли;
					
					СправочникОбъект.Наименование            = СтрЗаменить(Наименование,Символы.ПС,"/");
					СправочникОбъект.МеждународноеСокращение = СтрЗаменить(СтрокаУровень3.КодовоеБуквенноеОбозначениеМеждународное,Символы.ПС,"/");
					СправочникОбъект.НаименованиеПолное      = СтрЗаменить(СтрокаУровень3.Наименование,Символы.ПС,"/");
					СправочникОбъект.Код                     = СтрокаУровень3.КодЧисловой;
					СправочникОбъект.Записать();
	

				КонецЕсли;
				
			КонецЦикла;
			Н = Н + 1;
		КонецЦикла;
	КонецЦикла;
	ЗафиксироватьТранзакцию();

КонецПроцедуры

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//  Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//           где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы,
//           связанного с реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("GUID_Меркурий");
	Результат.Добавить("БазоваяЕдиницаИзмерения");
	Результат.Добавить("Коэффициент");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли