#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Возвращает список номенклатуры учетной системы по продукции
//
// Параметры:
//  Данные	 - СправочникСсылка	 - ссылка на справочник типа:
//  	СправочникСсылка.СвойстваЗаписейСкладскогоЖурнала, СправочникСсылка.Продукция.
//  Текст	 - Строка			 - значение автоподбора.
// 
// Возвращаемое значение:
//  СписокЗначений - список объектов справочника "Номенклатура учетной системы".
//
Функция НоменклатураУчетнойСистемыПоПродукции(Данные, Текст = "") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СоответствиеПродукцииНоменклатуреУчетнойСистемы.НоменклатураУчетнойСистемы КАК НоменклатураУчетнойСистемы
		|ИЗ
		|	РегистрСведений.СоответствиеПродукцииНоменклатуреУчетнойСистемы КАК СоответствиеПродукцииНоменклатуреУчетнойСистемы
		|ГДЕ
		|	СоответствиеПродукцииНоменклатуреУчетнойСистемы.Продукция = &Продукция
		|	И НЕ СоответствиеПродукцииНоменклатуреУчетнойСистемы.НоменклатураУчетнойСистемы.ПометкаУдаления";
	
	Если ТипЗнч(Данные) = Тип("СправочникСсылка.СвойстваЗаписейСкладскогоЖурнала") Тогда
		
		Запрос.УстановитьПараметр("Продукция", Данные.Продукция);
		
	Иначе
		
		Запрос.УстановитьПараметр("Продукция", Данные);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Текст) Тогда
		
		Запрос.Текст = Запрос.Текст + "
			|	И СоответствиеПродукцииНоменклатуреУчетнойСистемы.НоменклатураУчетнойСистемы.Наименование ПОДОБНО &Текст";
		
		Запрос.УстановитьПараметр("Текст", "%" + Текст + "%");
		
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СписокЗначений = Новый СписокЗначений;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокЗначений.Добавить(Выборка.НоменклатураУчетнойСистемы);
	КонецЦикла;
	
	Возврат СписокЗначений;
	
КонецФункции

// Возвращает список номенклатуры учетной системы по продукции
//
// Параметры:
//  Данные	 - СправочникСсылка	 - ссылка на справочник типа:
//  	СправочникСсылка.СвойстваЗаписейСкладскогоЖурнала, СправочникСсылка.Продукция.
// 
// Возвращаемое значение:
//  СписокЗначений - список объектов справочника "Номенклатура учетной системы".
//
Функция ПолучитьНоменклатуруУчетнойСитсемы(Данные) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	СоответствиеПродукцииНоменклатуреУчетнойСистемы.НоменклатураУчетнойСистемы КАК НоменклатураУчетнойСистемы
		|ИЗ
		|	РегистрСведений.СоответствиеПродукцииНоменклатуреУчетнойСистемы КАК СоответствиеПродукцииНоменклатуреУчетнойСистемы
		|ГДЕ
		|	СоответствиеПродукцииНоменклатуреУчетнойСистемы.Продукция = &Продукция
		|	И НЕ СоответствиеПродукцииНоменклатуреУчетнойСистемы.НоменклатураУчетнойСистемы.ПометкаУдаления";
	
	Если ТипЗнч(Данные) = Тип("СправочникСсылка.СвойстваЗаписейСкладскогоЖурнала") Тогда
		
		Запрос.УстановитьПараметр("Продукция", Данные.Продукция);
		
	Иначе
		
		Запрос.УстановитьПараметр("Продукция", Данные);
		
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Справочники.НоменклатураУчетнойСистемы.ПустаяСсылка();
		
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Возврат Выборка.НоменклатураУчетнойСистемы;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли