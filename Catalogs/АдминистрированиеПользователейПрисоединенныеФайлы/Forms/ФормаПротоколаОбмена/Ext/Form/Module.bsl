#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВладелецФайла = Параметры.ВладелецФайла;
	
	Если Параметры.ВладелецФайла = Документы.АдминистрированиеПользователей.ПустаяСсылка() Тогда
		ВызватьИсключение НСтр("ru = 'Протокол обмена возможно посмотреть
									  |только из формы документа.'");
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АдминистрированиеПользователейПрисоединенныеФайлы.ВладелецФайла КАК Документ,
		|	АдминистрированиеПользователейПрисоединенныеФайлы.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
		|	МАКСИМУМ(АдминистрированиеПользователейПрисоединенныеФайлы.ЭтапВыполненияОбмена) КАК ЭтапВыполненияОбмена
		|ПОМЕСТИТЬ ВТ_ПоследнииСообщения
		|ИЗ
		|	Справочник.АдминистрированиеПользователейПрисоединенныеФайлы КАК АдминистрированиеПользователейПрисоединенныеФайлы
		|ГДЕ
		|	АдминистрированиеПользователейПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
		|
		|СГРУППИРОВАТЬ ПО
		|	АдминистрированиеПользователейПрисоединенныеФайлы.ИдентификаторЗапроса,
		|	АдминистрированиеПользователейПрисоединенныеФайлы.ВладелецФайла
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АдминистрированиеПользователейПрисоединенныеФайлы.ВладелецФайла КАК Документ,
		|	АдминистрированиеПользователейПрисоединенныеФайлы.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
		|	АдминистрированиеПользователейПрисоединенныеФайлы.ИдентификаторЗаявки КАК ИдентификаторЗаявки,
		|	АдминистрированиеПользователейПрисоединенныеФайлы.ВидОперации КАК ВидОперации,
		|	АдминистрированиеПользователейПрисоединенныеФайлы.СтатусЗаявки КАК СтатусЗаявки,
		|	АдминистрированиеПользователейПрисоединенныеФайлы.КодСостоянияСервера КАК КодСостоянияСервера,
		|	АдминистрированиеПользователейПрисоединенныеФайлы.ДатаСоздания КАК Дата
		|ИЗ
		|	ВТ_ПоследнииСообщения КАК ВТ_ПоследнииСообщения
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.АдминистрированиеПользователейПрисоединенныеФайлы КАК АдминистрированиеПользователейПрисоединенныеФайлы
		|		ПО ВТ_ПоследнииСообщения.Документ = АдминистрированиеПользователейПрисоединенныеФайлы.ВладелецФайла
		|			И ВТ_ПоследнииСообщения.ИдентификаторЗапроса = АдминистрированиеПользователейПрисоединенныеФайлы.ИдентификаторЗапроса
		|			И ВТ_ПоследнииСообщения.ЭтапВыполненияОбмена = АдминистрированиеПользователейПрисоединенныеФайлы.ЭтапВыполненияОбмена
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата УБЫВ";
	
	Запрос.УстановитьПараметр("ВладелецФайла", Параметры.ВладелецФайла);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаСообщений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаблицаСообщенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеСтроки = ЭтотОбъект.Элементы.ТаблицаСообщений.ДанныеСтроки(ВыбраннаяСтрока);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Документ", ДанныеСтроки.Документ);
	ПараметрыФормы.Вставить("ИдентификаторЗапроса", ДанныеСтроки.ИдентификаторЗапроса);
	
	ОткрытьФорму("ОбщаяФорма.ФормаСообщенияПротоколаОбмена", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСообщенийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСообщенийПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти