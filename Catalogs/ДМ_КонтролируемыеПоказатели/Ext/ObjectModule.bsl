
Процедура ПередЗаписью(Отказ)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.ДатаСостояния = ТекущаяДата();
	ЭтотОбъект.УровеньВложенности = ЭтотОбъект.Уровень();
	ЭтотОбъект.ПолныйКодЭлемента = ЭтотОбъект.ПолныйКод();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЭтоНовый() Тогда
		
		лЗапрос = Новый Запрос(
		"ВЫБРАТЬ
		|	МАКСИМУМ(ДМ_КонтролируемыеПоказатели.КодСостояния) КАК КодСостояния
		|ИЗ
		|	Справочник.ДМ_КонтролируемыеПоказатели КАК ДМ_КонтролируемыеПоказатели
		|ГДЕ
		|	ДМ_КонтролируемыеПоказатели.Ссылка В ИЕРАРХИИ(&Родитель)
		|	И ДМ_КонтролируемыеПоказатели.Ссылка <> &Родитель"
		);
		лЗапрос.УстановитьПараметр( "Родитель", ЭтотОбъект.Ссылка );
		лРезультат = лЗапрос.Выполнить();
		лВыборка = лРезультат.Выбрать();
		Если лВыборка.Следующий() Тогда
		
			Если лВыборка.КодСостояния <> NULL И ЭтотОбъект.КодСостояния < лВыборка.КодСостояния Тогда
				ЭтотОбъект.КодСостояния = лВыборка.КодСостояния;
			КонецЕсли;
		
		КонецЕсли;
		
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта( ЭтотОбъект.Ссылка, "КодСостояния" ) <> ЭтотОбъект.КодСостояния Тогда
			ЭтотОбъект.ДатаИзмененияСостояния = ЭтотОбъект.ДатаСостояния;
		КонецЕсли;
		
	Иначе
		
		ЭтотОбъект.ДатаИзмененияСостояния = ЭтотОбъект.ДатаСостояния;
	
	КонецЕсли;
	
	Если ЗначениеЗаполнено( ЭтотОбъект.Родитель )
		И ЭтотОбъект.КодСостояния <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.Ссылка,"КодСостояния")
		Тогда
		
		ЭтотОбъект.ДополнительныеСвойства.Вставить("УстановитьКодСостояния", Истина );
		
	Иначе
		
		ЭтотОбъект.ДополнительныеСвойства.Вставить("УстановитьКодСостояния", Ложь );
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.ДатаСостояния = ТекущаяДата();
	
	Если ЗначениеЗаполнено( ЭтотОбъект.Родитель )
		И ЭтотОбъект.ДополнительныеСвойства.УстановитьКодСостояния = Истина
		Тогда
		
		лЗапрос = Новый Запрос(
		"ВЫБРАТЬ
		|	МАКСИМУМ(ДМ_КонтролируемыеПоказатели.КодСостояния) КАК КодСостояния,
		|	ДМ_КонтролируемыеПоказатели.Родитель КАК Родитель
		|ИЗ
		|	Справочник.ДМ_КонтролируемыеПоказатели КАК ДМ_КонтролируемыеПоказатели
		|ГДЕ
		|	ДМ_КонтролируемыеПоказатели.Родитель = &Родитель
		|
		|СГРУППИРОВАТЬ ПО
		|	ДМ_КонтролируемыеПоказатели.Родитель"
		);
		лЗапрос.УстановитьПараметр( "Родитель", ЭтотОбъект.Родитель );
		лРезультат = лЗапрос.Выполнить();
		лВыборка = лРезультат.Выбрать();
		Если лВыборка.Следующий() Тогда
		
			лРодитель = ЭтотОбъект.Родитель.ПолучитьОбъект();
			лРодитель.Заблокировать();
			лРодитель.КодСостояния = лВыборка.КодСостояния;
			лРодитель.Записать();
			лРодитель.Разблокировать();
		
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры
