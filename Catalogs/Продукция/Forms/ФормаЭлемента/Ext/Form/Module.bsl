#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Свойство("Основание") И Параметры.Основание <> Неопределено Тогда
		НоменклатураУчетнойСистемы = Параметры.Основание;
		ЗаполнитьЗначенияСвойств(Объект, НоменклатураУчетнойСистемы,, "Код");
		Объект.НаименованиеПолное = НоменклатураУчетнойСистемы.Наименование;
	КонецЕсли;
	
	УстановитьСвойстваЭлементовФормы()
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(НоменклатураУчетнойСистемы) Тогда
		СтруктураЗаписи = Новый Структура();
		
		СтруктураЗаписи.Вставить("Продукция", 				   Объект.Ссылка);
		СтруктураЗаписи.Вставить("НоменклатураУчетнойСистемы", НоменклатураУчетнойСистемы);
		
		РегистрыСведений.СоответствиеПродукцииНоменклатуреУчетнойСистемы.ДобавитьЗапись(СтруктураЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеДанныхПродукции" Тогда
		ЭтаФорма.Прочитать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.НаименованиеПолное) Тогда
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПолучателяПриИзменении(Элемент)
	
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТабличнойЧастиПроизводителиПродукции

&НаКлиенте
Процедура ПроизводителиПродукцииПредприятиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияУВСКлиент.ПредприятиеНачалоВыбора(Элемент, СтандартнаяОбработка,, Объект.Контрагент, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	Элементы.ГруппаСвойстваПродукцииПолучателя.Видимость = Объект.ПродукцияПолучателя;
	
КонецПроцедуры

#КонецОбласти