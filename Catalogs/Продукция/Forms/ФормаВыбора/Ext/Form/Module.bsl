#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Контрагент") Тогда
		КонтрагентОтбор = Параметры.Контрагент;
	КонецЕсли;
	
	Если Параметры.Свойство("Предприятие") Тогда
		ПредприятиеОтбор = Параметры.Предприятие;
	КонецЕсли;
	
	Элементы.КонтрагентОтбор.Видимость  = ЗначениеЗаполнено(КонтрагентОтбор);
	Элементы.ПредприятиеОтбор.Видимость = ЗначениеЗаполнено(ПредприятиеОтбор);
	
	ГруппаОтбора = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(Список.Отбор.Элементы,
		"ГруппаОтбора", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, "Контрагент",
		ВидСравненияКомпоновкиДанных.Равно, КонтрагентОтбор, "Контрагент", ЗначениеЗаполнено(КонтрагентОтбор));
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, "ПроизводителиПродукции.Предприятие",
		ВидСравненияКомпоновкиДанных.Равно, ПредприятиеОтбор, "ПроизводителиПродукцииПредприятие", ЗначениеЗаполнено(ПредприятиеОтбор));
		
	Если Параметры.Свойство("ЗарегистрированнаяПродукция") Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, "GUID_Меркурий",
			ВидСравненияКомпоновкиДанных.НеРавно, "", "GUID_Меркурий", Параметры.ЗарегистрированнаяПродукция);
	КонецЕсли;
		
	Если Параметры.Свойство("ВидПродукции") Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, "Владелец.Владелец",
			ВидСравненияКомпоновкиДанных.Равно, Параметры.ВидПродукции, "ВидПродукции", ЗначениеЗаполнено(Параметры.ВидПродукции));
	КонецЕсли;
	
	Если Параметры.Свойство("СобственнаяПродукция") Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, "СобственнаяПродукция.Продукция",
			ВидСравненияКомпоновкиДанных.Равно, Параметры.СобственнаяПродукция, "СобственнаяПродукция", ЗначениеЗаполнено(Параметры.СобственнаяПродукция));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти