#Область ОбработчикиСобытийФорм

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ИспользоватьКонтрольПартийПриНезавершенномПроизводстве") Тогда
		ИспользоватьКонтрольПартийПриНезавершенномПроизводстве = Параметры.ИспользоватьКонтрольПартийПриНезавершенномПроизводстве;
	КонецЕсли;
	
	Если ИспользоватьКонтрольПартийПриНезавершенномПроизводстве Тогда
		
		УстановитьТекстЗапросаПриКонтролеПартий();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьТекстЗапросаПриКонтролеПартий()

	Список.ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СправочникНомераПроизводственныхПартий.Ссылка КАК Ссылка,
		|	СправочникНомераПроизводственныхПартий.ПометкаУдаления КАК ПометкаУдаления,
		|	СправочникНомераПроизводственныхПартий.Код КАК Код,
		|	СправочникНомераПроизводственныхПартий.Наименование КАК Наименование,
		|	СправочникНомераПроизводственныхПартий.Предопределенный КАК Предопределенный,
		|	СправочникНомераПроизводственныхПартий.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.НомераПроизводственныхПартий КАК СправочникНомераПроизводственныхПартий
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроизводственныеПартии.СрезПоследних(
		|				,
		|				Организация = &Организация
		|					И Предприятие = &Предприятие) КАК ПроизводственныеПартииСрезПоследних
		|		ПО СправочникНомераПроизводственныхПартий.Ссылка = ПроизводственныеПартииСрезПоследних.НомерПартии
		|ГДЕ
		|	(ПроизводственныеПартииСрезПоследних.ИдентификаторТранзакции = &ИдентификаторТранзакции
		|				И НЕ ПроизводственныеПартииСрезПоследних.ЗавершеноПроизводство
		|			ИЛИ ПроизводственныеПартииСрезПоследних.НомерПартии ЕСТЬ NULL)";
	
	Если Параметры.Свойство("Организация") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Организация", Параметры.Организация);
	КонецЕсли;
	
	Если Параметры.Свойство("Предприятие") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Предприятие", Параметры.Предприятие);
	КонецЕсли;
	
	Если Параметры.Свойство("ИдентификаторТранзакции") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ИдентификаторТранзакции", Параметры.ИдентификаторТранзакции);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
