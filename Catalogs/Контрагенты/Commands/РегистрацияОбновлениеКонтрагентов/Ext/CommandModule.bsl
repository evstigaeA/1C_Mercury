
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Организация = ОбщегоНазначенияУВСВызовСервера.ОрганизацияТекущегоПользователя();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("БезПользователя", Истина);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МассивКонтрагентов", ПараметрКоманды);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводДополнительныхЗначенийЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВводаДополнительныхЗначений", ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения);
	
КонецПроцедуры
&НаКлиенте
Процедура ВводДополнительныхЗначенийЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Или Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыполненияОперации = ИнтеграцияВетисAPIВызовСервера.ОбновитьДанныеПоКонтрагентам(ДополнительныеПараметры.МассивКонтрагентов, Результат.Организация, Результат.Пользователь, Результат.ОснованиеДобавленияИзменения);
	
	Если ЗначениеЗаполнено(РезультатВыполненияОперации.ОписаниеОшибки) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Операция не выполнена:'") + РезультатВыполненияОперации.ОписаниеОшибки);

	Иначе
		
		ТекстОшибки = НСтр("ru = 'При выполнении операции по хоз. субъекту <%1> произошли ошибки:
							|%2'");

		ТекстУспешногоВыполнения = НСтр("ru = 'Выполнение операции по хоз. субъекту <%1> прошло успешно.'");
		
		ДанныеРезультата = РезультатВыполненияОперации.Контрагент;
		
		Для Каждого ТекРезультат Из ДанныеРезультата Цикл
			
			Если ЗначениеЗаполнено(ТекРезультат.ОписаниеОшибки) Тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ТекРезультат.Контрагент, ТекРезультат.ОписаниеОшибки), ТекРезультат.Контрагент);
							
			Иначе
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстУспешногоВыполнения, ТекРезультат.Контрагент), ТекРезультат.Контрагент);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Оповестить("ОбновлениеДанныхКонтрагента");
	
КонецПроцедуры

#КонецОбласти

