#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбновлениеИнформационнойБазы

// Монопольный обработчик обновления 2.0.6.1
Процедура ИзменитьИдентификаторСервиса() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПодключенияКВетисAPI.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НастройкиПодключенияКВетисAPI КАК НастройкиПодключенияКВетисAPI";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НастройкаПодключенияОбъект = Выборка.Ссылка.ПолучитьОбъект();
		НастройкаПодключенияОбъект.ИдентификаторСервиса = "mercury-g2b.service:2.1";
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НастройкаПодключенияОбъект);
		Исключение
			ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстИсключения = НСтр("ru = 'Не удалось выполнить изменение идентификатора сервиса  по причине: %ПредставлениеОшибки%.'");
			ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПредставлениеОшибки%", ПредставлениеОшибки);
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Справочники.НастройкиПодключенияКВетисAPI,
				Выборка.Ссылка,
				ТекстИсключения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли