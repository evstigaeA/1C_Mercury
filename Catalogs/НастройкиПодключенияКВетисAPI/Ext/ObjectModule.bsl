#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Только для внутреннего использования
Функция НастройкаПодключенияУникальна()
	
	Если ПометкаУдаления Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Истина
	КонецЕсли;
	
	ТекущаяНастройкаУникальна = Истина;
	
	// Проверка на уникальное использование настройки по реквизитам: Организация
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущаяНастройка", Ссылка);
	Запрос.УстановитьПараметр("Организация", Организация);
		
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиПодключения.Организация
	|ИЗ
	|	Справочник.НастройкиПодключенияКВетисAPI КАК НастройкиПодключения
	|ГДЕ
	|	НЕ НастройкиПодключения.ПометкаУдаления
	|	И НастройкиПодключения.Организация = &Организация
	|	И НастройкиПодключения.Ссылка <> &ТекущаяНастройка";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ТекущаяНастройкаУникальна = Ложь;
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ШаблонСообщения = НСтр("ru = 'В информационной базе уже существует настройка подключения к Ветис.API
									|для организации %1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Выборка.Организация);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТекущаяНастройкаУникальна;
	
КонецФункции

// Только для внутреннего использования
Функция GUIDКонтрагентаСовпадаетСУказаннымВНастройках()
	
	Если ПометкаУдаления Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Истина
	КонецЕсли;
	
	ТекущийGUIDКонтрагентаСовпадаетСУказаннымВНастройках = Истина;
	                                  
	Если Не ИдентификаторХозяйствующегоСубъекта = Контрагент.GUID_Меркурий Тогда
		ТекущийGUIDКонтрагентаСовпадаетСУказаннымВНастройках = Ложь;
		
		ШаблонСообщения = НСтр("ru = 'Идентификатор хозяйствующего субъекта %1 
								|не совпадает с указанным в настройках подключения к Ветис.API'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, Контрагент);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
	Возврат ТекущийGUIDКонтрагентаСовпадаетСУказаннымВНастройках;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НастройкаПодключенияУникальна() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Не GUIDКонтрагентаСовпадаетСУказаннымВНастройках() Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли