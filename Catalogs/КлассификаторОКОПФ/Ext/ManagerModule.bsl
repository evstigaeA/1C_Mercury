#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//  Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//           где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы,
//           связанного с реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("GUID_Меркурий");
	
	Возврат Результат;
	
КонецФункции

// Заполнение справочника КлассификаторОКОПФ на основании макета справочника
//
Процедура ЗагрузитьЭлементы() Экспорт
	
	Макет = ПолучитьМакет("ОКОПФ");
	
	ВысотаМакета = Макет.ВысотаТаблицы;
	
	ПоляСтруктуры = Новый Структура("GUID_Меркурий, Код, Наименование");
	
	Для ТекСтрока = 1  По ВысотаМакета Цикл
		
		ПоляСтруктуры.GUID_Меркурий = СокрЛП(Макет.Область("R"+ТекСтрока+"C1").Текст);
		ПоляСтруктуры.Код		 	= СокрЛП(Макет.Область("R"+ТекСтрока+"C2").Текст);
		ПоляСтруктуры.Наименование 	= СокрЛП(Макет.Область("R"+ТекСтрока+"C3").Текст);
		
		НайденныйКодОКОПФ = Справочники.КлассификаторОКОПФ.НайтиПоРеквизиту("GUID_Меркурий", ПоляСтруктуры.GUID_Меркурий);
		
		Если НайденныйКодОКОПФ.Пустая() Тогда
			
			КодОКОПФОбъект = Справочники.КлассификаторОКОПФ.СоздатьЭлемент();
			
		Иначе
			
			КодОКОПФОбъект = НайденныйКодОКОПФ.ПолучитьОбъект();
			Попытка
				КодОКОПФОбъект.Заблокировать();
			Исключение
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не удалось заблокировать для записи элемент %1.
				|Возможно он редактируется другим пользователем.
				|Заполнение кодов ОКОПФ остановлено.'"), ПоляСтруктуры.GUID_Меркурий);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,КодОКОПФОбъект.Ссылка);
				Возврат;
				
			КонецПопытки;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(КодОКОПФОбъект, ПоляСтруктуры);
		
		Если КодОКОПФОбъект.Модифицированность() Тогда
			
			КодОКОПФОбъект.ОбменДанными.Загрузка = Истина;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(КодОКОПФОбъект);
			
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Представление = Данные.Код + " - " + Данные.Наименование;
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Код");
	Поля.Добавить("Наименование");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли