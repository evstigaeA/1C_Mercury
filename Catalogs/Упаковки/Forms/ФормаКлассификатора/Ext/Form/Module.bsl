
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ДанныеИзМакета = Справочники.Упаковки.ПолучитьДанныеИзМакета();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеИзМакета.GUID_Меркурий,
		|	ДанныеИзМакета.Наименование,
		|	ДанныеИзМакета.КодЕЭК,
		|	ДанныеИзМакета.СтароеНаименование,
		|	ДанныеИзМакета.НеИспользуется
		|ПОМЕСТИТЬ ВТ_Упаковки
		|ИЗ
		|	&ДанныеИзМакета КАК ДанныеИзМакета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Упаковки.GUID_Меркурий,
		|	ВТ_Упаковки.Наименование,
		|	ВТ_Упаковки.КодЕЭК,
		|	ВТ_Упаковки.СтароеНаименование,
		|	ВТ_Упаковки.НеИспользуется,
		|	НЕ Упаковки.Ссылка ЕСТЬ NULL КАК Загружено,
		|	ЛОЖЬ КАК Загружать,
		|	ЕСТЬNULL(Упаковки.Ссылка, ЗНАЧЕНИЕ(Справочник.Упаковки.ПустаяСсылка)) КАК Ссылка
		|ИЗ
		|	ВТ_Упаковки КАК ВТ_Упаковки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Упаковки КАК Упаковки
		|		ПО ВТ_Упаковки.GUID_Меркурий = Упаковки.GUID_Меркурий";
	
	Запрос.УстановитьПараметр("ДанныеИзМакета", ДанныеИзМакета);	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаУпаковок.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	ОбработатьРезультатыПодбораНаСервере();
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого ТекСтрока Из ТаблицаУпаковок Цикл
		ТекСтрока.Загружать = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого ТекСтрока Из ТаблицаУпаковок Цикл
		ТекСтрока.Загружать = Ложь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ОбработатьРезультатыПодбораНаСервере()
	НачатьТранзакцию();
	
	Для Каждого СтрокаУпаковка Из ТаблицаУпаковок Цикл
		
		Если Не СтрокаУпаковка.Загружать Тогда
			Продолжить;	
		КонецЕсли;
		
		Если СтрокаУпаковка.Загружено Тогда
			СправочникОбъект = СтрокаУпаковка.Ссылка.ПолучитьОбъект();
		Иначе
			СправочникОбъект = Справочники.Упаковки.СоздатьЭлемент();
			СправочникОбъект.УстановитьНовыйКод();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СправочникОбъект, СтрокаУпаковка);
		СправочникОбъект.ОбменДанными.Загрузка = Истина;
		СправочникОбъект.Записать();
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
КонецПроцедуры

#КонецОбласти