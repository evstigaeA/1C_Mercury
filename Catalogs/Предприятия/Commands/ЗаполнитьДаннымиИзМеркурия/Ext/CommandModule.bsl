
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	МассивПредприятий = ПараметрКоманды;
	
	РезультатВыполненияОперации = ИнтеграцияВетисAPIВызовСервера.ПолучитьАктуальныеДанныеПоПредприятию(МассивПредприятий);
	
	Если ЗначениеЗаполнено(РезультатВыполненияОперации.ОписаниеОшибки) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Операция не выполнена:'") + РезультатВыполненияОперации.ОписаниеОшибки);

	Иначе
		
		ТекстОшибки = НСтр("ru = 'При выполнении операции по предприятию <%1> произошли ошибки:
							|%2'");

		ТекстУспешногоВыполнения = НСтр("ru = 'Выполнение операции по предприятию <%1> прошло успешно.'");
		
		ДанныеРезультата = РезультатВыполненияОперации.Предприятие;
		
		Для Каждого ТекРезультат Из ДанныеРезультата Цикл
			
			Если ЗначениеЗаполнено(ТекРезультат.ОписаниеОшибки) Тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ТекРезультат.Предприятие, ТекРезультат.ОписаниеОшибки), ТекРезультат.Предприятие);
							
			Иначе
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстУспешногоВыполнения, ТекРезультат.Предприятие), ТекРезультат.Предприятие);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Оповестить("ОбновлениеДанныхПредприятия");
	
КонецПроцедуры

#КонецОбласти