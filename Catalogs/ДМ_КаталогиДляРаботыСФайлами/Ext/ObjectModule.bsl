Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ПустаяСтрока( КаталогФайловДляОбработки ) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДМ_КаталогиДляРаботыСФайлами.Ссылка КАК Ссылка,
		|	ДМ_КаталогиДляРаботыСФайлами.Код КАК Код,
		|	ДМ_КаталогиДляРаботыСФайлами.Наименование КАК Наименование,
		|	ДМ_КаталогиДляРаботыСФайлами.КаталогФайловДляОбработки КАК КаталогФайловДляОбработки,
		|	ДМ_КаталогиДляРаботыСФайлами.МаскаФайла КАК МаскаФайла,
		|	ДМ_КаталогиДляРаботыСФайлами.КаталогАрхиваОбработанныхФайлов КАК КаталогАрхиваОбработанныхФайлов,
		|	ДМ_КаталогиДляРаботыСФайлами.Предопределенный КАК Предопределенный,
		|	ДМ_КаталогиДляРаботыСФайлами.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.ДМ_КаталогиДляРаботыСФайлами КАК ДМ_КаталогиДляРаботыСФайлами
		|ГДЕ
		|	ДМ_КаталогиДляРаботыСФайлами.КаталогФайловДляОбработки = &КаталогФайловДляОбработки
		|	И (ДМ_КаталогиДляРаботыСФайлами.МаскаФайла = &МаскаФайла
		|			ИЛИ &ПустаяМаскаФайла)
		|	И ДМ_КаталогиДляРаботыСФайлами.Ссылка <> &Ссылка";
		
		Запрос.Параметры.Вставить("КаталогФайловДляОбработки", СокрЛП(КаталогФайловДляОбработки) );
		Запрос.Параметры.Вставить("МаскаФайла", МаскаФайла);
		Запрос.Параметры.Вставить("ПустаяМаскаФайла", ПустаяСтрока( МаскаФайла ));
		Запрос.Параметры.Вставить("Ссылка", Ссылка);
		
		мВК = Новый Массив;
		мВК.Добавить(Справочники.ДМ_КаталогиДляРаботыСФайлами.ВнутреннееПеремещение);
		мВК.Добавить(Справочники.ДМ_КаталогиДляРаботыСФайлами.Клиент);
		
		мВК2 = Новый Массив;
		мВК2.Добавить(Справочники.ДМ_КаталогиДляРаботыСФайлами.ГашениеВСД);
		мВК2.Добавить(Справочники.ДМ_КаталогиДляРаботыСФайлами.Производство);
		
		ВыбЗапрВК = Запрос.Выполнить().Выбрать();
		Если ВыбЗапрВК.Следующий() Тогда
			Если (мВК.Найти(Ссылка) <> Неопределено И мВК.Найти(ВыбЗапрВК.Ссылка) <> Неопределено)
				ИЛИ (мВК2.Найти(Ссылка) <> Неопределено И мВК2.Найти(ВыбЗапрВК.Ссылка) <> Неопределено) Тогда
			Иначе
				//разрешены одинаковый каталог и маска для внутренних перемещений и клиентских отгрузок
				лТекстСообщения = НСтр("ru = 'Каталог %1 и маска для файлов %2 уже указаны в другом элементе справочника'");
				лТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( лТекстСообщения, СокрЛП(КаталогФайловДляОбработки), СокрЛП(МаскаФайла) );
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю( лТекстСообщения );
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ Справочники.ДМ_КаталогиДляРаботыСФайлами.КаталогОбрабатываемыхФайловДоступен( КаталогФайловДляОбработки ) Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока( КаталогАрхиваОбработанныхФайлов ) Тогда
		
		Если Не Справочники.ДМ_КаталогиДляРаботыСФайлами.КаталогАрхиваОбработанныхФайловДоступен( КаталогАрхиваОбработанныхФайлов ) Тогда
			Отказ = Истина;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	КаталогФайловДляОбработки		= СокрЛП( КаталогФайловДляОбработки );
	МаскаФайла						= СокрЛП( МаскаФайла );
	КаталогАрхиваОбработанныхФайлов = СокрЛП( КаталогАрхиваОбработанныхФайлов );
	
КонецПроцедуры
