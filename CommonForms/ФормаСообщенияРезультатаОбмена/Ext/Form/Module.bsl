#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ВидОперации") Тогда
		ВидОперации = Параметры.ВидОперации;
	КонецЕсли;
	Если Параметры.Свойство("ИдентификаторЗапроса") Тогда
		ИдентификаторЗапроса = Параметры.ИдентификаторЗапроса;
	КонецЕсли;
	Если Параметры.Свойство("ОшибкиВыполненияОбмена") Тогда
		ОшибкиВыполнения = Параметры.ОшибкиВыполненияОбмена;
	КонецЕсли;
	
	СтрокиСообщения = Новый Массив;
	Если ЗначениеЗаполнено(ОшибкиВыполнения) Тогда
		
		Элементы.КартинкаРезультата.Картинка = БиблиотекаКартинок.Ошибка32;
		
		СтрокиСообщения.Добавить(СтрЗаменить(НСтр("ru = 'Выполнение операции ""%1"" завершено с ошибками:'"), "%1", ВидОперации));
		СтрокиСообщения.Добавить(Символы.ПС);
		СтрокиСообщения.Добавить(Новый ФорматированнаяСтрока(ОшибкиВыполнения,, ЦветаСтиля.ЦветОсобогоТекста));
		
	Иначе
		
		СтрокиСообщения.Добавить(СтрЗаменить(НСтр("ru = 'Выполнение операции ""%1"" завершено успешно.'"), "%1", ВидОперации));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗапроса) Тогда
		СтрокиСообщения.Добавить(Символы.ПС);
		СтрокиСообщения.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Техническая информация'"),, ЦветаСтиля.ГиперссылкаЦвет,, "ОткрытьСообщениеОбмена"));
	КонецЕсли;
	
	ОписаниеРезультата = Новый ФорматированнаяСтрока(СтрокиСообщения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОписаниеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторЗапроса", ИдентификаторЗапроса);
	
	ОткрытьФорму("ОбщаяФорма.ФормаСообщенияПротоколаОбмена", ПараметрыФормы, Элемент);
	
КонецПроцедуры

#КонецОбласти