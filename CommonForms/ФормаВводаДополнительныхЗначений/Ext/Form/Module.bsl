#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
	КонецЕсли;
	
	Если Параметры.Свойство("БезОрганизации") Тогда
		БезОрганизации = Параметры.БезОрганизации;
	КонецЕсли;
	
	Если Параметры.Свойство("БезПользователя") Тогда
		БезПользователя = Параметры.БезПользователя;
	КонецЕсли;
	
	Если Параметры.Свойство("БезОснования") Тогда
		БезОснования = Параметры.БезОснования;
	КонецЕсли;
	
	Если Параметры.Свойство("ОснованиеДобавленияИзменения") Тогда
		ОснованиеДобавленияИзменения = Параметры.ОснованиеДобавленияИзменения;
	КонецЕсли;
	
	Элементы.Организация.Видимость = Не БезОрганизации;
	Элементы.Пользователь.Видимость = Не БезПользователя;
	Элементы.ОснованиеДобавленияИзменения.Видимость = Не БезОснования;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Отказ = Ложь;
	
	Если Не БезОснования Тогда
		Если Не ЗначениеЗаполнено(ОснованиеДобавленияИзменения) Тогда
			ТекстОшибки = НСтр("ru = 'Не указано основание добавления/изменения записи в ИС ""Меркурий"".'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"ОснованиеДобавленияИзменения",,Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Не БезОрганизации Тогда
		Если Не ЗначениеЗаполнено(Организация) Тогда
			ТекстОшибки = НСтр("ru = 'Не указана организация-инициатор добавления/изменения записи в ИС ""Меркурий"".'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"Организация",,Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Не БезПользователя Тогда
		Если Не ЗначениеЗаполнено(Пользователь) Тогда
			ТекстОшибки = НСтр("ru = 'Не указан пользователь-инициатор добавления/изменения записи в ИС ""Меркурий"".'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,"Пользователь",,Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Не Отказ Тогда
		Результат = Новый Структура();
		Результат.Вставить("Организация", ?(ЗначениеЗаполнено(Организация), Организация, Неопределено));
		Результат.Вставить("Пользователь", ?(ЗначениеЗаполнено(Пользователь), Пользователь, Неопределено));
		Результат.Вставить("ОснованиеДобавленияИзменения", ОснованиеДобавленияИзменения);
		
		Закрыть(Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти