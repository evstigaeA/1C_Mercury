#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("РеквизитыКонтрагентов") Тогда
		
		ЗаполнитьДаннымиКонтрагентов(Параметры.РеквизитыКонтрагентов);
		
	Иначе
		
		Отказ = Истина; //Ограничение по функционалу. данные уже должны быть все полученны
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКонтрагенты

&НаКлиенте
Процедура КонтрагентыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыполнитьВыборКонтрагента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьКонтрагента(Команда)
	
	ВыполнитьВыборКонтрагента();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьВыборКонтрагента()
	
	ТекДанные = Элементы.Контрагенты.ТекущиеДанные;
	ОповеститьОВыборе(ТекДанные);
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьДаннымиКонтрагентов(РеквизитыКонтрагентов) 
	
	КоличествоНайденных = РеквизитыКонтрагентов.КоличествоНайденных;
	
	Для Каждого РеквизитыОрганизации Из РеквизитыКонтрагентов.РеквизитыОрганизаций Цикл
		НоваяСтрока = Контрагенты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, РеквизитыОрганизации);
		Если РеквизитыОрганизации.Адрес <> Неопределено Тогда
			НоваяСтрока.ЮридическийАдрес = РеквизитыОрганизации.Адрес.Представление;
		КонецЕсли;
	КонецЦикла;
		
	Если КоличествоНайденных > 0 Тогда
		ТекущийЭлемент = Элементы.Контрагенты;
		Элементы.Контрагенты.ТекущаяСтрока = Контрагенты[0].ПолучитьИдентификатор();
	Иначе
		ТекущийЭлемент = Элементы.ИНН;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти