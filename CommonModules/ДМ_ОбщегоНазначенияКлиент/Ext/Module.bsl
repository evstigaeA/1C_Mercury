
Процедура УстановитьПринтерПоУмолчанию(ИмяПринтера, КодОшибки = Неопределено) Экспорт
		
	ТекстОшибки = "";
	КодОшибки = -1;
	
	Попытка
		
		ТекстСкрипта = ДМ_СлужебныйВызовСервера.ПолучитьОбщийМакетSetDefaultPrinter_ps1();
		ИмяФайла = ПолучитьИмяВременногоФайла("ps1");
		
		ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла);
		ЗаписьТекста.Записать(ТекстСкрипта);
		ЗаписьТекста.Закрыть();
		
		КомандаPowershell = "&{try { &'" + ИмяФайла + "' -PrinterName \""" + ИмяПринтера + "\"" } catch { $LastExitCode=-2 }; exit $LastExitCode}";
		Команда = "PowerShell -NoProfile -NonInteractive -ExecutionPolicy Unrestricted -Command """ + КомандаPowershell + """";
		
		Shell = Новый COMОбъект("WScript.Shell");
		КодОшибки = Shell.Run(Команда, 0, True);
		Если КодОшибки <> 0 Тогда
			ТекстОшибки = "Код ошибки: " + КодОшибки;
		КонецЕсли;
		
	Исключение		
		ТекстОшибки = ОписаниеОшибки();
		КодОшибки = -1;
	КонецПопытки;
	
	УдалитьФайлы(ИмяФайла);
	
	Если КодОшибки <> 0 Тогда
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПринтерПоУмолчаниюНаОсновеДоступныхСкладов() Экспорт

	Если ДМ_СлужебныйВызовСервера.ЕстьРоль( "ПолныеПрава" ) Тогда
	    Возврат;
	КонецЕсли;
	
	лПринтеры = ДМ_СлужебныйВызовСервера.ПолучитьСписокПринтеровДляПользователя();
	лКоличествоПринтеров = лПринтеры.Количество();
	Если лКоличествоПринтеров = 0 Тогда
		ТекстСообщенияПользователю = "Список принтеров пользователя пустой! Принтер по умолчанию не установлен.";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияПользователю);
		ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации("Ошибка установки принтера по умолчанию", "Ошибка", ТекстСообщенияПользователю,, Истина);
		Возврат;
	КонецЕсли;
	
	УстановленПринтерПоУмолчанию = Ложь;
	ТекстСообщения = "";
	
	Попытка
		
		Network = Новый COMОбъект("WScript.Network");
		УстановленныеПринтеры = Новый Соответствие;
		
		NetworkPrinters = Network.EnumPrinterConnections();
		Для Сч = 0 По NetworkPrinters.Length - 1 Цикл
			УстановленныеПринтеры.Вставить(СокрЛП(NetworkPrinters.Item(Сч+1)), Истина);
			Сч = Сч + 1;
		КонецЦикла;
		
		Для н=1 По лКоличествоПринтеров Цикл
			
			Принтер = СокрЛП(лПринтеры[н-1]);
			
			Если УстановленныеПринтеры[Принтер] = Неопределено Тогда 
			
				Попытка			
					Network.AddWindowsPrinterConnection(Принтер);
				Исключение
					лОписаниеОшибки = ОписаниеОшибки();
					ТекстСообщения = ТекстСообщения + ?(ТекстСообщения="", "", Символы.ПС) + "Принтер """ + Принтер + """ - ошибка добавления принтера в систему: " + Символы.ПС + лОписаниеОшибки;
				КонецПопытки;
				
			КонецЕсли;
			
			Если НЕ УстановленПринтерПоУмолчанию Тогда
			
				Попытка			
					УстановитьПринтерПоУмолчанию(Принтер);
					УстановленПринтерПоУмолчанию = Истина;
				Исключение
					лОписаниеОшибки = ОписаниеОшибки();
					ТекстСообщения = ТекстСообщения + ?(ТекстСообщения="", "", Символы.ПС) + "Принтер """ + Принтер + """ - ошибка установки принтера по умолчанию: " + Символы.ПС + лОписаниеОшибки;
				КонецПопытки;		
				
			КонецЕсли;
			
		КонецЦикла;
	
	Исключение
		ТекстСообщения = ОписаниеОшибки();
	КонецПопытки;
	
	Если Не УстановленПринтерПоУмолчанию Тогда
		ТекстСообщенияПользователю = "Не удалось установить принтер по умолчанию!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияПользователю);
		ТекстСообщения = ТекстСообщенияПользователю + Символы.ПС + ТекстСообщения;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстСообщения) Тогда		
		ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации("Ошибки при добавлении принтеров", "Ошибка", ТекстСообщения,, Истина);
	КонецЕсли;
	
КонецПроцедуры
