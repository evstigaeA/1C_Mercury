////////////////////////////////////////////////////////////////////////////////
// Менеджер защиты: Система лицензирования отраслевых и специализированных решений. 
// Поддержка программного интерфейса СЛК
// Область выполнения: Сервер (вызов сервера)
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Проверяет лицензию для текущего сеанса ИБ, в случае успеха возвращает Истина,
// в случае ошибки - Ложь. Информация об ошибке возвращается в ОписаниеОшибки
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//                 
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
//  КомандаМенеджераЗащиты  - Строка - Команда, выполняемая менеджером защиты
//							Поддерживаемые команды:
//							 - ПодключитьЕслиНеЗапущен
//							 - Отключить
//
// Возвращаемое значение:
//   Булево   - в случае успеха возвращает Истина, в случае ошибки - Ложь
//
Функция ПроверитьЛицензиюСеанса(Знач Серия, ОписаниеОшибки = "", Знач КомандаМенеджераЗащиты = "ПодключитьЕслиНеЗапущен")	Экспорт

	Попытка
		
		ТолькоНаличиеКлюча = Ложь;
		СерияЗащитыИнициализирована = слкМенеджерЗащитыСервер.СерияЗащитыИнициализирована(Серия, ТолькоНаличиеКлюча);
		
		Если СерияЗащитыИнициализирована Тогда
			
			Если КомандаМенеджераЗащиты = "Отключить" Тогда
				слкМенеджерЗащитыСервер.УдалитьМенеджерСерииЗащиты(Серия, ОписаниеОшибки);
				Возврат Ложь;
			Иначе
				МенеджерОбъектов = ПолучитьМенеджерОбъектовСерииЗащиты(Серия);
				Лицензия = МенеджерОбъектов.ПопыткаПолучитьЛицензию();
				Если Лицензия = Неопределено Тогда
					ОписаниеОшибки = МенеджерОбъектов.ПолучитьОписаниеОшибки();
					Возврат Ложь;
				Иначе
					Возврат Истина;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			
			Если КомандаМенеджераЗащиты = "ПодключитьЕслиНеЗапущен" Тогда
				МенеджерОбъектов = ПолучитьМенеджерОбъектовСерииЗащиты(Серия);
				Если слкМенеджерЗащитыСервер.СерияЗащитыИнициализирована(Серия, ТолькоНаличиеКлюча) Тогда
					Лицензия = МенеджерОбъектов.ПопыткаПолучитьЛицензию();
					Если Лицензия = Неопределено Тогда
						ОписаниеОшибки = МенеджерОбъектов.ПолучитьОписаниеОшибки();
						Возврат Ложь;
					Иначе
						Возврат Истина;
					КонецЕсли;
				Иначе
					Возврат Ложь;
				КонецЕсли;
			Иначе
				Возврат Ложь;
			КонецЕсли;
		
		КонецЕсли;
		
	Исключение
		
		ОписаниеОшибки = ОписаниеОшибки();
		Возврат Ложь;
		
	КонецПопытки;

КонецФункции // ПроверитьЛицензиюСеанса()

// Возвращает защищенный объект по его имени в файле данных, в случае ошибки – Неопределено. 
// Информация об ошибке возвращается в ОписаниеОшибки
// Для создания объектов, находящихся в дополнительных файлах данных, 
// необходимо указать псевдоним нужного файла, например:
//	// объект из основного файла XXXX.datafile
//	Объект = МенеджерОбъектов.СоздатьОбъект("ЗащищеннаяОбработка"); 
//	// объект в файле XXXX.Extension.datafile
//	Объект = МенеджерОбъектов.СоздатьОбъект("Extension.ЗащищеннаяОбработка"); 
// Для создания объекта из файла данных, загруженного из макета в конфигурации, 
// в качестве псевдонима указывается имя макета, например:
//	// объект в файле из макета "ДанныеСЛК"
//	Объект = МенеджерОбъектов.СоздатьОбъект("ДанныеСЛК.ЗащищеннаяОбработка");
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//                 
//	Имя - Строка - Имя объекта в файле данных
//                 
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Объект, Неопределено   - защищенный объект по его имени в файле данных, 
//                            в случае ошибки – Неопределено 
//
Функция СоздатьОбъект(Знач Серия, Знач Имя, ОписаниеОшибки = "")	Экспорт
	
	Возврат слкМенеджерЗащитыПовтИсп.СоздатьОбъект(Серия, Имя);
	
КонецФункции // СоздатьОбъект()

// Возвращает серийный номер ключа, лицензию которого занимает текущий сеанс, 
// в случае ошибки – Неопределено. Информация об ошибке возвращается в ОписаниеОшибки
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//                 
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Строка, Неопределено   - серийный номер ключа, лицензию которого занимает 
//                            текущий сеанс, в случае ошибки – Неопределено
//
Функция ПолучитьНомерКлюча(Знач Серия, ОписаниеОшибки = "") Экспорт
	
	Попытка
		МенеджерОбъектов = ПолучитьМенеджерОбъектовСерииЗащиты(Серия);
		
		НомерКлюча = МенеджерОбъектов.ПопыткаПолучитьНомерКлюча();
		Если НомерКлюча = Неопределено Тогда
			ВызватьИсключениеСЛК(МенеджерОбъектов.ПолучитьОписаниеОшибки(), Серия);
		КонецЕсли;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат НомерКлюча;

КонецФункции // ПолучитьНомерКлюча()

// Возвращает в виде строки список установленных на сервере СЛК ключей используемой серии.
// Строка имеет следующий формат:
// 	СН1,Тип1,Лицензии1,ТипУстройства1,РегНомер1;СН2,Тип2,Лицензии2,ТипУстройства1,РегНомер1;…
//	Где:
// 		СН – серийный номер ключа
//		Тип – тип ключа (3 – основной, 4 – дополнительный)
//		Лицензии – количество лицензий
//		ТипУстройства – 0 – аппаратный, 1 – программный
//		РегНомер – регистрационный номер, связанный с С/Н (0, если не связан)
// Информация об ошибке возвращается в ОписаниеОшибки.
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//                 
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Строка	- список установленных на сервере СЛК ключей используемой серии 
//
Функция ПолучитьСписокКлючей(Знач Серия, ОписаниеОшибки = "")Экспорт
	
	Попытка
		МенеджерОбъектов = ПолучитьМенеджерОбъектовСерииЗащиты(Серия);
		
		Лицензии = МенеджерОбъектов.ПопыткаПолучитьЛицензии();
		Если Лицензии = Неопределено Тогда
		    ВызватьИсключениеСЛК(МенеджерОбъектов.ПолучитьОписаниеОшибки(), Серия);
		КонецЕсли;
		
		МассивЛицензий = Новый Массив;
		Для Каждого ТекЛицензия Из Лицензии Цикл
			ОписаниеЛицензии = ОписаниеЛицензии(ТекЛицензия, Истина);
			МассивЛицензий.Добавить(ОписаниеЛицензии);
		КонецЦикла;
		
		СписокКлючей = СтрСоединить(МассивЛицензий, Символы.ПС);
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат СписокКлючей;

КонецФункции // ПолучитьСписокКлючей()

// Возвращает значение указанного параметра для текущего ключа защиты, с которым работает система, 
// в случае ошибки – пустая строка. Информация об ошибке возвращается в ОписаниеОшибки
//
// Если для текущего ключа значение не определено и параметр ИспользоватьОсновнойКлюч равен Ложь, 
// то будет возвращено значение параметра по умолчанию.
// Если для текущего ключа значение не определено и параметр ИспользоватьОсновнойКлюч равен Истина, 
// то будет возвращено значение для основного ключа. Если же значение для основного ключа не определено, 
// то будет возвращено значение параметра по умолчанию.
// В качестве основного ключа будет выступать ключ, указанный в файле параметров как главный 
// основной ключ или первый подключенный к компьютеру основной.
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//	Имя - Строка - Имя параметра в файле параметров
//	ИспользоватьОсновнойКлюч - Булево - Признак использования основного ключа.
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Строка   - значение указанного параметра для текущего ключа защиты
//
Функция ПолучитьЗначениеПараметра(Знач Серия, Знач Имя, Знач ИспользоватьОсновнойКлюч = Истина, ОписаниеОшибки = "") Экспорт
	
	Попытка
		МенеджерОбъектов = ПолучитьМенеджерОбъектовСерииЗащиты(Серия);
		
		ЗначениеПараметра = МенеджерОбъектов.ПопыткаПолучитьПараметр(Серия, Имя, ИспользоватьОсновнойКлюч);
		Если ЗначениеПараметра = Неопределено Тогда
			ВызватьИсключениеСЛК(МенеджерОбъектов.ПолучитьОписаниеОшибки(), Серия);
		КонецЕсли;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат ЗначениеПараметра;

КонецФункции // ПолучитьЗначениеПараметра()

// Возвращает значение пользовательской памяти для текущего ключа защиты, с 
// которым работает система, в случае ошибки вызывается исключение. По умолчанию пароль
// на чтение равен пустой строке. Изменить пароли можно при помощи редактора файлов данных
// и функций УстановитьПаролиНаПамятьКлюча и ПопыткаУстановитьПаролиНаПамятьКлюча.
// Информация об ошибке возвращается в ОписаниеОшибки
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//                 
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Строка   - значение пользовательской памяти для текущего ключа защиты 
//				(максимум 8 символов)
//
Функция ПрочитатьПамятьКлюча(Знач Серия, Знач Пароль, ОписаниеОшибки = "") Экспорт
	
	Попытка
		МенеджерОбъектов = ПолучитьМенеджерОбъектовСерииЗащиты(Серия);
		
		ПамятьКлюча = МенеджерОбъектов.ПопыткаПрочитатьПамять(Серия, Пароль);
		Если ПамятьКлюча = Неопределено Тогда
			ВызватьИсключениеСЛК(МенеджерОбъектов.ПолучитьОписаниеОшибки(), Серия);
		КонецЕсли;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат ПамятьКлюча;

КонецФункции // ПрочитатьПамятьКлюча()

// Записывает значение в пользовательскую память текущего ключа защиты, с которым 
// работает система. По умолчанию пароль на запись равен пустой строке. Изменить 
// пароли можно при помощи редактора файлов данных и функций УстановитьПаролиНаПамятьКлюча 
// и ПопыткаУстановитьПаролиНаПамятьКлюча.
// Информация об ошибке возвращается в ОписаниеОшибки
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//	Пароль - Строка - Пароль на запись (максимум 8 символов)
//	Значение - Строка - Данные для записи в память ключа (максимум 400 символов)
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Булево   - успешность записи памяти
//
Функция ЗаписатьПамятьКлюча(Знач Серия, Знач Пароль, Знач Значение, ОписаниеОшибки = "")	Экспорт
	
	Попытка
		МенеджерОбъектов = ПолучитьМенеджерОбъектовСерииЗащиты(Серия);
		
		ПамятьЗаписана = МенеджерОбъектов.ПопыткаЗаписатьПамять(Серия, Пароль, Значение);
		Если НЕ ПамятьЗаписана Тогда
			ВызватьИсключениеСЛК(МенеджерОбъектов.ПолучитьОписаниеОшибки(), Серия);
		КонецЕсли;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат ПамятьЗаписана;

КонецФункции // ЗаписатьПамятьКлюча()

// Обнуляет пользовательскую память текущего ключа защиты, с которым работает система. 
// Пароли на чтение и запись устанавливаются равными пустой строке.
// Информация об ошибке возвращается в ОписаниеОшибки
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//                 
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Булево   - успешность обнуления памяти ключа
//
Функция ОбнулитьПамятьКлюча(Знач Серия, ОписаниеОшибки = "")	Экспорт
	
	Попытка
		МенеджерОбъектов = ПолучитьМенеджерОбъектовСерииЗащиты(Серия);
		
		ПамятьОбнулена = МенеджерОбъектов.ПопыткаОбнулитьПамять(Серия);
		Если НЕ ПамятьОбнулена Тогда
			ВызватьИсключениеСЛК(МенеджерОбъектов.ПолучитьОписаниеОшибки(), Серия);
		КонецЕсли;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат ПамятьОбнулена;

КонецФункции // ОбнулитьПамятьКлюча()

// Устанавливает пароли на доступ к пользовательской памяти текущего ключа защиты, 
// с которым работает система.
// Информация об ошибке возвращается в ОписаниеОшибки
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//	НаЧтениеСтарый - Строка - Текущий пароль на чтение (максимум 8 символов)
//	НаЗаписьСтарый - Строка - Текущий пароль на запись (максимум 8 символов)
//	НаЧтениеНовый - Строка - Новый пароль на чтение (максимум 8 символов)
//	НаЗаписьНовый - Строка - Новый пароль на запись (максимум 8 символов)
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Булево   - успешность установки пароля
//
Функция УстановитьПаролиНаПамятьКлюча(Знач Серия, Знач НаЧтениеСтарый, Знач НаЗаписьСтарый, Знач НаЧтениеНовый, Знач НаЗаписьНовый, ОписаниеОшибки = "")	Экспорт
	
	Попытка
		МенеджерОбъектов = ПолучитьМенеджерОбъектовСерииЗащиты(Серия);
		
		ПаролиУстановлены = МенеджерОбъектов.УстановитьПароли(Серия, НаЧтениеСтарый, НаЗаписьСтарый, НаЧтениеНовый, НаЗаписьНовый);
		Если НЕ ПаролиУстановлены Тогда
			ВызватьИсключениеСЛК(МенеджерОбъектов.ПолучитьОписаниеОшибки(), Серия);
		КонецЕсли;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат ПаролиУстановлены;

КонецФункции // УстановитьПаролиНаПамятьКлюча()

// Информация об ошибке возвращается в ОписаниеОшибки
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Структура - значения структуры
//      Licences - Количество ключей
//      Connections - Количество соединений
//      Sessions - Количество сеансов
//      TotalLicenceCount - Общее количество лицензий
//      UseLicenceCount - Количество использованных лицензий
//      FreeLicenceCount - Количество свободных лицензий
Функция ПолучитьЗначенияСчетчиковЛицензий(Знач Серия, ОписаниеОшибки = "") Экспорт
	
	Попытка
		МенеджерОбъектов = ПолучитьМенеджерОбъектовСерииЗащиты(Серия);
		
		ЗначенияСчетчиковЛицензий = МенеджерОбъектов.ПопыткаПолучитьСчетчики();
		Если ЗначенияСчетчиковЛицензий = Неопределено Тогда
			ВызватьИсключениеСЛК(МенеджерОбъектов.ПолучитьОписаниеОшибки(), Серия);
		КонецЕсли;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Возврат 0;
	КонецПопытки;
	
	Возврат ЗначенияСчетчиковЛицензий;

КонецФункции // ПолучитьОбщееКоличествоЛицензий()

// Освобождает лицензию сеанса в клиент-серверном варианте работы
// В клиент-серверном варианте работы при завершении работы конфигурации 
// компонента остается загруженной в процессе сервера 1С и, если ей не указать, 
// продолжает занимать лицензии сеансов
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК. Если не указана
//					 освобождаются все занятые лицензии СЛК.
//                 
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Булево   - результат удаления менеджера серии защиты
//
Функция ОсвободитьЛицензиюСеанса(Знач Серия = Неопределено, ОписаниеОшибки = "")	Экспорт
	
	Возврат слкМенеджерЗащитыСервер.УдалитьМенеджерСерииЗащиты(Серия, ОписаниеОшибки);

КонецФункции // ОсвободитьЛицензиюСеанса()

// Возвращает в виде строки список регистрационных номеров ключей используемой серии 
// установленных на сервере СЛК. Информация об ошибке возвращается в ОписаниеОшибки.
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//                 
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//                 
//	ОтборПоТипуКлюча - Число,Неопределено - Регистрационные номера с учетом отбора 
//						по типу ключей:
//						3 – основной, 4 – дополнительный, неопределено - без отбора
//
// Возвращаемое значение:
//   Строка	- список регистрационных номеров ключей  используемой серии 
//				установленных на сервере СЛК. Строка имеет следующий формат:
//				РегНомер1;РегНомер2;…
//				Где: РегНомер – регистрационный номер, связанный с С/Н 
//								(пустая строка, если не связан)
//
Функция ПолучитьРегНомераКлючей(Знач Серия, ОписаниеОшибки = "", Знач ОтборПоТипуКлюча = Неопределено) Экспорт
	
	Попытка
		МенеджерОбъектов = ПолучитьМенеджерОбъектовСерииЗащиты(Серия);
		
		МассивЛицензий = МенеджерОбъектов.ПопыткаПолучитьЛицензии();
		Если МассивЛицензий = Неопределено Тогда
			слкМенеджерЗащиты.ВызватьИсключениеСЛК(МенеджерОбъектов.ПолучитьОписаниеОшибки(), Серия);
		КонецЕсли;
		
		СписокКлючей = "";
		Для Каждого ТекЛицензия Из МассивЛицензий Цикл
			СписокКлючей = СписокКлючей + ТекЛицензия.KeyNo + ","
				+ ТекЛицензия.KeyType + ","
				+ ТекЛицензия.LicenceCount + ","
				+ ТекЛицензия.Type + ","
				+ ТекЛицензия.RegNo + ";";
		КонецЦикла;
		
		СписокКлючей = Лев(СписокКлючей, СтрДлина(СписокКлючей) - 1);
		
		Если ОтборПоТипуКлюча = Неопределено Тогда
			КлючиЗащиты = слкМенеджерЗащитыПовтИсп.ТаблицаИзСпискаКлючейЗащиты(СписокКлючей);
		Иначе
			КлючиЗащиты = слкМенеджерЗащитыПовтИсп.ТаблицаИзСпискаКлючейЗащиты(СписокКлючей).Скопировать(Новый Структура("Тип", ОтборПоТипуКлюча));
		КонецЕсли;
		
		Если КлючиЗащиты = Неопределено Тогда
			Возврат "";
		Иначе
			Возврат СтрСоединить(КлючиЗащиты.ВыгрузитьКолонку("РегНомер"), ";");
		КонецЕсли;
		
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Возврат "";
	КонецПопытки;

КонецФункции // ПолучитьРегНомераКлючей()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает актуальность версии компоненты СЛК 
//
// Параметры:
//	Версия - Строка - минимальное значение версии
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Булево   - Верия компоненты СЛК актуальна
//
Функция ПроверитьВерсиюКомпонентыСЛК(Знач Версия, ОписаниеОшибки = "") Экспорт
	
	Если слкМенеджерЗащитыСервер.ПодключитьКомпонентуСЛК( ,ОписаниеОшибки) Тогда
		МенеджерЛицензий = Новый("AddIn.Licence.LicenceExtension");
		Возврат МенеджерЛицензий.ПроверитьВерсию(Версия);
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Регламентная процедура проверки лицензий сеанса. Актуально для нескольких 
// баз размещенных на одном сервере приложений при клиент-серверном взаимодействии
//
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Булево   - в случае успеха возвращает Истина, в случае ошибки - Ложь
// 
Функция ПроверитьЛицензииСеанса(ОписаниеОшибки = "")	Экспорт
	
	Результат = Истина;
	
	МенеджерЗащиты = слкМенеджерЗащитыСервер.ПолучитьМенеджерЗащиты(Ложь);
	Для каждого СерияКлючейЗащиты Из МенеджерЗащиты Цикл
		Если НЕ ПроверитьЛицензиюСеанса(СерияКлючейЗащиты.Ключ, ОписаниеОшибки) Тогда
			Результат = Ложь;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

// Версия используемого модуля компоненты СЛК
//
// Параметры:
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//
// Возвращаемое значение:
//   Строка   - Верия компоненты СЛК
//
Функция ВерсияКомпонентыСЛК(ОписаниеОшибки = "") Экспорт
	
	Если слкМенеджерЗащитыСервер.ПодключитьКомпонентуСЛК( ,ОписаниеОшибки) Тогда
		МенеджерЛицензий = Новый("AddIn.Licence.LicenceExtension");
		Возврат МенеджерЛицензий.Версия;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Сообщение об ошибке СЛК с возможностью указания серии защиты
// При выполнении очищает параметр Описание ошибки
//
// Параметры:
//  ОписаниеОшибки  - Строка - Информация об ошибке 
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//						(может не указываться)
//
Процедура СообщитьОбОшибкеСЛК(Знач ОписаниеОшибки, Серия = "")	Экспорт

	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
					
		ИмяСобытия = НСтр("ru = 'Ошибка лицензирования '");

		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = ИмяСобытия + ?(ЗначениеЗаполнено(Серия), "(" + Серия + ")", "")
		 + ": " + ОписаниеОшибки;
		 
		 Сообщение.Сообщить();

		Если Метаданные.ОбщиеМодули.Найти("ЖурналРегистрацииВызовСервера") <> Неопределено Тогда
			
			СтруктураСообщения = Новый Структура;
			СтруктураСообщения.Вставить("ИмяСобытия", ИмяСобытия);
			СтруктураСообщения.Вставить("ПредставлениеУровня", "Ошибка");
			СтруктураСообщения.Вставить("Комментарий", ОписаниеОшибки);
			
			СообщенияДляЖурналаРегистрации = Новый СписокЗначений;
			СообщенияДляЖурналаРегистрации.Добавить(СтруктураСообщения);
			
			Выполнить("ЖурналРегистрацииВызовСервера.ЗаписатьСобытияВЖурналРегистрации(СообщенияДляЖурналаРегистрации)");
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОписаниеОшибки = "";
		
КонецПроцедуры

// Вызов исключения ошибки СЛК с возможностью указания серии защиты
// При выполнении очищает параметр Описание ошибки
//
// Параметры:
//  ОписаниеОшибки  - Строка - Информация об ошибке 
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//						(может не указываться)
//
Процедура ВызватьИсключениеСЛК(Знач ОписаниеОшибки, Серия = "")	Экспорт

	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
					
		ИмяСобытия = НСтр("ru = 'Ошибка лицензирования '");

		ТекстИсключения = ИмяСобытия + ?(ЗначениеЗаполнено(Серия), "(" + Серия + ")", "")
		 + ": " + ОписаниеОшибки;
		 
		 ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	ОписаниеОшибки = "";
		
КонецПроцедуры

// Состояние лицензии
Функция СостояниеЛицензии(Лицензия)
	
	Если Лицензия.Enabled Тогда
		Возврат "Доступен";
	ИначеЕсли Лицензия.Expired Тогда
		Возврат "Срок действия истек";
	ИначеЕсли НЕ Лицензия.Workable Тогда
		Возврат "Недоступен: " + ?(Лицензия.Type = "USB", "Аппаратный ключ неисправен", "Изменена конфигурация компьютера");
	Иначе
		Возврат "Недоступен (?)";
	КонецЕсли;
	
КонецФункции

Функция ТипКлюча(Тип)
	
	Если Тип = 3 Тогда
		Возврат "Основной"
	ИначеЕсли Тип = 4 Тогда
		Возврат "Дополнительный"
	ИначеЕсли Тип = 5 Тогда
		Возврат "Демонстрационный"
	Иначе
		Возврат "Неизвестный тип (" + Строка(Тип) + ")";
	КонецЕсли;
	
КонецФункции

// Краткое описание лицензии
Функция ОписаниеЛицензии(Лицензия, ПоказатьСостояние = Истина)
	
	Стр = 
		"С/Н " + Формат(Лицензия.KeyNo, "ЧГ=0") + ", " + 
		?(Лицензия.Type = "USB", Лицензия.Type, "Программный") + ", ";
	
	Если Лицензия.IsBlank Тогда
		Стр = Стр + 
		"Неактивированный ключ (болванка)";
	Иначе
		Стр = Стр + 
			ТипКлюча(Лицензия.KeyType) + " (Лицензий: " + Формат(Лицензия.LicenceCount, "ЧГ=0") + ")";
	КонецЕсли;
	
	Стр = Стр + 
		?(ПоказатьСостояние, " - " + СостояниеЛицензии(Лицензия), "");
	
	Возврат Стр;
КонецФункции

#Если Сервер Тогда

// Возвращает менеджер серии защиты. Структура, содержащая МенеджерОбъектов СЛК и 
// описывающая  параметры подключения серии защиты.
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//  КомандаМенеджераЗащиты  - Строка - Команда, выполняемая менеджером защиты
//							Поддерживаемые команды:
//							 - ПодключитьЕслиНеЗапущен
//							 - Отключить
//                 
// Возвращаемое значение:
//   Структурв   - Менеджер серии защиты
//
Функция ПолучитьМенеджерСерииЗащиты(Знач Серия, ОписаниеОшибки = "")	Экспорт
	
	Возврат слкМенеджерЗащитыПовтИсп.ПолучитьМенеджерСерииЗащиты(Серия);

КонецФункции // ПолучитьМенеджерСерииЗащиты()

// Возвращает менеджер объектов серии защиты.
//
// Параметры:
//	Серия - Строка - Уникальная серия ключей защиты СЛК
//  ОписаниеОшибки  - Строка - Информация об ошибке полученная вызовом функции 
//						СЛК ПолучитьОписаниеОшибки (описание последней ошибки)
//  КомандаМенеджераЗащиты  - Строка - Команда, выполняемая менеджером защиты
//							Поддерживаемые команды:
//							 - ПодключитьЕслиНеЗапущен
//							 - Отключить
//                 
// Возвращаемое значение:
//   Структурв   - Менеджер серии защиты
//
Функция ПолучитьМенеджерОбъектовСерииЗащиты(Знач Серия, ОписаниеОшибки = "")	Экспорт
	
	Возврат слкМенеджерЗащитыПовтИсп.МенеджерОбъектовСерииЗащиты(Серия);

КонецФункции // ПолучитьМенеджерСерииЗащиты()

#КонецЕсли

#КонецОбласти
