
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Параметры = Новый Структура;
	Параметры.Вставить("МассивВСД"   , ПараметрКоманды);
	Параметры.Вставить("Пользователь", Пользователь);
	
	ДлительнаяОперация = ОбновитьДанныеВСД(Параметры, Новый УникальныйИдентификатор);
	
	Контекст = Новый Структура;
	Контекст.Вставить("АдресРезультатаОбновленияВСД", ДлительнаяОперация.АдресРезультата);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Происходит обновление данных ВСД...'");
	ПараметрыОжидания.ВыводитьОкноОжидания       = Истина;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	
	Оповещение = Новый ОписаниеОповещения("ПослеОбновленияДанныхВСД", ЭтотОбъект, Контекст);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ОбновитьДанныеВСД(Параметры, Идентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Идентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление данных ВСД.'");

	ФоновоеЗадание = ДлительныеОперации.ВыполнитьВФоне("ИнтеграцияВетисAPIОбработкаПартий.ОбновитьДанныеВСДВФоновомРежиме",
		Параметры, ПараметрыВыполнения);
		
	Возврат ФоновоеЗадание;
КонецФункции

&НаКлиенте
Процедура ВывестиСообщениеОбОшибке(Знач ТекстОшибки)
	ОчиститьСообщения();
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПослеОбновленияДанныхВСД(Результат, Контекст) Экспорт

	// Ответ: 
	// - Структура - Выполнено - результат в структуре.
	// - Неопределено - Отменено пользователем.
	Если Результат = Неопределено Тогда
		
		// Освобождаем выделенную память.
		УдалитьИзВременногоХранилища(Контекст.АдресРезультатаОбновленияВСД); // Вызов сервера.
		Возврат;
	КонецЕсли;
	
	// В контексте АдресРезультата больше не нужен, теперь он актуализирован в Результат.АдресРезультата.
	Контекст.Удалить("АдресРезультатаОбновленияВСД");
	
	// Результат.Статус:
	// - "Ошибка" - описание в ОписаниеОшибки.
	// - "Выполнено" - результат в Результат.АдресРезультата.
	Если Результат.Статус = "Выполнено" Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РезультатОперации", Результат.АдресРезультата);
		ПараметрыФормы.Вставить("ЗаголовокФормы"   , НСтр("ru = 'Результат обновления данных по ВСД'"));
		
		ОткрытьФорму("ОбщаяФорма.ФормаРезультатаЗагрузки", ПараметрыФормы);
		
	Иначе
		
		// Освобождаем выделенную память.
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить обновление данных ВСД
			    |по причине:
				|%1'"), Результат.ПодробноеПредставлениеОшибки);
			
		ВывестиСообщениеОбОшибке(ТекстОшибки);
	КонецЕсли;

КонецПроцедуры
