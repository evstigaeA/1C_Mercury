#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ПараметрыКонтроляПартий") Тогда
		ПараметрыКонтроляПартий = Параметры.ПараметрыКонтроляПартий;
	КонецЕсли;
	
	ПредставлениеДатыВыработки = ИнтеграцияВетисAPIКлиентСервер.ПредставлениеДатыПоУмолчанию();
	ПредставлениеСрокаГодности = ИнтеграцияВетисAPIКлиентСервер.ПредставлениеДатыПоУмолчанию();
	
	РеквизитыЗаполнения = Новый Структура;
	РеквизитыЗаполнения.Вставить("НекачественныйГруз");
	РеквизитыЗаполнения.Вставить("НомерПартии");
	РеквизитыЗаполнения.Вставить("ОсобыеОтметки");
	РеквизитыЗаполнения.Вставить("ОсуществленКонтрольВрачом");
	РеквизитыЗаполнения.Вставить("ПроводиласьВетсанэкспертиза");
	РеквизитыЗаполнения.Вставить("СкоропортящаясяПродукция");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПредставлениеДатыВыработкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИнтеграцияВетисAPIКлиент.ОткрытьФормуЗаданияДат(ЭтотОбъект, ЭтотОбъект, Элемент, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеСрокаГодностиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИнтеграцияВетисAPIКлиент.ОткрытьФормуЗаданияДат(ЭтотОбъект, ЭтотОбъект, Элемент, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерПартииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФорму("Справочник.НомераПроизводственныхПартий.ФормаВыбора", ПараметрыКонтроляПартий, Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	СтруктураДанных = Новый Структура;
	Для Каждого ТекРеквизит Из РеквизитыЗаполнения Цикл
		Если УстанавливатьНеЗаполненыеЗначения Или ЗначениеЗаполнено(ЭтотОбъект[ТекРеквизит.Ключ]) Тогда
			
			Если (ТекРеквизит.Ключ = "ОсуществленКонтрольВрачом" Или ТекРеквизит.Ключ = "СкоропортящаясяПродукция" Или ТекРеквизит.Ключ = "НекачественныйГруз")
				И Не ЗначениеЗаполнено(ЭтотОбъект[ТекРеквизит.Ключ]) Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураДанных.Вставить(ТекРеквизит.Ключ, ЭтотОбъект[ТекРеквизит.Ключ]);
			
		КонецЕсли;
	КонецЦикла;
	
	Если УстанавливатьНеЗаполненыеЗначения Или ЗначениеЗаполнено(ФорматДатыВыработки) Тогда
		ДанныеПоДатамВыработки = Новый Структура;
		ДанныеПоДатамВыработки.Вставить("ФорматДатыВыработки"       , ФорматДатыВыработки);
		ДанныеПоДатамВыработки.Вставить("ДатаВыработкиНачало"       , ДатаВыработкиНачало);
		ДанныеПоДатамВыработки.Вставить("ДатаВыработкиОкончание"    , ДатаВыработкиОкончание);
		СтруктураДанных.Вставить("ДанныеПоДатамВыработки", ДанныеПоДатамВыработки);
	КонецЕсли;
	
	Если УстанавливатьНеЗаполненыеЗначения Или ЗначениеЗаполнено(ФорматСрокаГодности) Тогда
		ДанныеПоСрокамГодности = Новый Структура;
		ДанныеПоСрокамГодности.Вставить("ФорматСрокаГодности"                , ФорматСрокаГодности);
		ДанныеПоСрокамГодности.Вставить("ДатаОкончанияСрокаГодностиНачало"   , ДатаОкончанияСрокаГодностиНачало);
		ДанныеПоСрокамГодности.Вставить("ДатаОкончанияСрокаГодностиОкончание", ДатаОкончанияСрокаГодностиОкончание);
		СтруктураДанных.Вставить("ДанныеПоСрокамГодности", ДанныеПоСрокамГодности);
	КонецЕсли;
	
	Закрыть(СтруктураДанных);

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти