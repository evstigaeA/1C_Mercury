#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Гражданство  = Справочники.СтраныМира.Россия;
	ТипДокумента = Справочники.ТипыТТН.ПаспортГражданинаРФ;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьЗакрыть(Команда)
	
	ТекстОшибки = "";
	
	Если ПроверитьЗаполнение() Тогда
		
		СоздатьНовогоПользователя(ТекстОшибки);
		
		Если ПустаяСтрока(ТекстОшибки) Тогда
			Сообщить(НСтр("ru = 'Создан новый пользователь'"));
			Закрыть(ПользовательМеркурия);
		Иначе
			Сообщить(НСтр("ru = 'Ошибка записи нового пользователя'") + " " + ТекстОшибки, СтатусСообщения.Важное);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьНовогоПользователя(ТекстОшибки)
	
	НовыйПользователь = Справочники.ПользователиСистемыМеркурий.СоздатьЭлемент();
	
	НовыйПользователь.Наименование      = Фамилия + " " + Имя + " " + Отчество;
	НовыйПользователь.Фамилия           = Фамилия;
	НовыйПользователь.Имя               = Имя;
	НовыйПользователь.Отчество          = Отчество;
	НовыйПользователь.ДатаРождения      = ДатаРождения;
	НовыйПользователь.СтраховойНомерПФР = СтраховойНомерПФР;
	НовыйПользователь.Гражданство       = Гражданство;
	НовыйПользователь.ТипДокумента      = ТипДокумента;
	НовыйПользователь.Серия             = Серия;
	НовыйПользователь.Номер             = Номер;
	НовыйПользователь.Должность         = Должность;
	НовыйПользователь.СобственныйПользователь = Истина;
	
	КонтактнаяИнформация = УправлениеКонтактнойИнформацией.НоваяКонтактнаяИнформация(Ложь);
	
	НоваяСтрока = КонтактнаяИнформация.Добавить();
	НоваяСтрока.Представление = EmailПользователя;
	НоваяСтрока.Вид = Справочники.ВидыКонтактнойИнформации.EmailПользователяСистемыМеркурий;
	НоваяСтрока.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
	
	Если ЗначениеЗаполнено(РабочийEmailПользователя) Тогда
		НоваяСтрока = КонтактнаяИнформация.Добавить();
		НоваяСтрока.Представление = РабочийEmailПользователя;
		НоваяСтрока.Вид = Справочники.ВидыКонтактнойИнформации.РабочийEmailПользователяСистемыМеркурий;
		НоваяСтрока.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТелефонПользователя) Тогда
		НоваяСтрока = КонтактнаяИнформация.Добавить();
		НоваяСтрока.Представление = ТелефонПользователя;
		НоваяСтрока.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонПользователяСистемыМеркурий;
		НоваяСтрока.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РабочийТелефонПользователя) Тогда
		НоваяСтрока = КонтактнаяИнформация.Добавить();
		НоваяСтрока.Представление = РабочийТелефонПользователя;
		НоваяСтрока.Вид = Справочники.ВидыКонтактнойИнформации.РабочийТелефонПользователяСистемыМеркурий;
		НоваяСтрока.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
	КонецЕсли;
	
	УправлениеКонтактнойИнформацией.УстановитьКонтактнуюИнформациюОбъекта(НовыйПользователь, КонтактнаяИнформация);
	УправлениеКонтактнойИнформациейСлужебный.ОбновитьКонтактнуюИнформациюДляСписковДляОбъекта(НовыйПользователь);
	
	НачатьТранзакцию();
	Попытка
		НовыйПользователь.Записать();
		ЗафиксироватьТранзакцию();
		ПользовательМеркурия = НовыйПользователь.Ссылка;
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти