#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат
	КонецЕсли;
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	ДополнительныеСвойства.Вставить("БылПроведен", Проведен);
	ДополнительныеСвойства.Вставить("ДатаСтарая",  Дата);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Ложь);
	
	Если ДополнительныеСвойства.БылПроведен И ДополнительныеСвойства.ДатаСтарая < Дата Тогда
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ТаблицаЗаписейСкладскогоЖурнала.Очистить();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Движения.ДвижениеПродукции.Записывать = Истина;
	Движения.ДвижениеПродукции.Очистить();
	Если ДополнительныеСвойства.ДатаДокументаСдвинутаВперед Тогда
		Движения.ДвижениеПродукции.Записать();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КорректировкаОстатковЗаписейСкладскогоЖурналаТаблицаЗаписейСкладскогоЖурнала.Ссылка.Организация,
		|	КорректировкаОстатковЗаписейСкладскогоЖурналаТаблицаЗаписейСкладскогоЖурнала.Ссылка.Предприятие,
		|	КорректировкаОстатковЗаписейСкладскогоЖурналаТаблицаЗаписейСкладскогоЖурнала.ЗаписьСкладскогоЖурнала,
		|	СУММА(КорректировкаОстатковЗаписейСкладскогоЖурналаТаблицаЗаписейСкладскогоЖурнала.Количество) КАК Количество
		|ПОМЕСТИТЬ ДокТЧ
		|ИЗ
		|	Документ.КорректировкаОстатковЗаписейСкладскогоЖурнала.ТаблицаЗаписейСкладскогоЖурнала КАК КорректировкаОстатковЗаписейСкладскогоЖурналаТаблицаЗаписейСкладскогоЖурнала
		|ГДЕ
		|	КорректировкаОстатковЗаписейСкладскогоЖурналаТаблицаЗаписейСкладскогоЖурнала.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаОстатковЗаписейСкладскогоЖурналаТаблицаЗаписейСкладскогоЖурнала.Ссылка.Предприятие,
		|	КорректировкаОстатковЗаписейСкладскогоЖурналаТаблицаЗаписейСкладскогоЖурнала.ЗаписьСкладскогоЖурнала,
		|	КорректировкаОстатковЗаписейСкладскогоЖурналаТаблицаЗаписейСкладскогоЖурнала.Ссылка.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокТЧ.ЗаписьСкладскогоЖурнала
		|ИЗ
		|	ДокТЧ КАК ДокТЧ";
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДвижениеПродукции");
	ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
	ЭлементБлокировки.УстановитьЗначение("Предприятие", Предприятие);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаписьСкладскогоЖурнала", "ЗаписьСкладскогоЖурнала");
	Блокировка.Заблокировать();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДвижениеПродукцииОстатки.Организация КАК Организация,
		|	ДвижениеПродукцииОстатки.Предприятие КАК Предприятие,
		|	ДвижениеПродукцииОстатки.ЗаписьСкладскогоЖурнала КАК ЗаписьСкладскогоЖурнала,
		|	ДвижениеПродукцииОстатки.КоличествоОстаток КАК Количество
		|ПОМЕСТИТЬ Остатки
		|ИЗ
		|	РегистрНакопления.ДвижениеПродукции.Остатки(
		|			&МоментВремени,
		|			(Организация, Предприятие, ЗаписьСкладскогоЖурнала) В
		|				(ВЫБРАТЬ
		|					ДокТЧ.Организация,
		|					ДокТЧ.Предприятие,
		|					ДокТЧ.ЗаписьСкладскогоЖурнала
		|				ИЗ
		|					ДокТЧ КАК ДокТЧ)) КАК ДвижениеПродукцииОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА Остатки.Количество > 0
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	КОНЕЦ КАК ВидДвижения,
		|	Остатки.Организация КАК Организация,
		|	Остатки.Предприятие КАК Предприятие,
		|	Остатки.ЗаписьСкладскогоЖурнала КАК ЗаписьСкладскогоЖурнала,
		|	ВЫБОР
		|		КОГДА Остатки.Количество < 0
		|			ТОГДА -Остатки.Количество
		|		ИНАЧЕ Остатки.Количество
		|	КОНЕЦ КАК Количество
		|ПОМЕСТИТЬ ДвиженияПоОстаткам
		|ИЗ
		|	Остатки КАК Остатки
		|ГДЕ
		|	Остатки.Количество <> 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ДвиженияПоОстаткам.ВидДвижения КАК ВидДвижения,
		|	ДвиженияПоОстаткам.Организация КАК Организация,
		|	ДвиженияПоОстаткам.Предприятие КАК Предприятие,
		|	ДвиженияПоОстаткам.ЗаписьСкладскогоЖурнала КАК ЗаписьСкладскогоЖурнала,
		|	СУММА(ДвиженияПоОстаткам.Количество) КАК Количество
		|ИЗ
		|	ДвиженияПоОстаткам КАК ДвиженияПоОстаткам
		|
		|СГРУППИРОВАТЬ ПО
		|	ДвиженияПоОстаткам.ВидДвижения,
		|	ДвиженияПоОстаткам.Организация,
		|	ДвиженияПоОстаткам.Предприятие,
		|	ДвиженияПоОстаткам.ЗаписьСкладскогоЖурнала
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		|	ДокТЧ.Организация,
		|	ДокТЧ.Предприятие,
		|	ДокТЧ.ЗаписьСкладскогоЖурнала,
		|	ДокТЧ.Количество
		|ИЗ
		|	ДокТЧ КАК ДокТЧ
		|ГДЕ
		|	ДокТЧ.Количество > 0";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Период", Дата);

	Движения.ДвижениеПродукции.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

#КонецОбласти

#КонецЕсли