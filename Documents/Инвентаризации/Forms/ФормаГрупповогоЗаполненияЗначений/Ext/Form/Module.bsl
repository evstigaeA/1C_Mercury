#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПредставлениеДатыВыработки = ИнтеграцияВетисAPIКлиентСервер.ПредставлениеДатыПоУмолчанию();
	ПредставлениеСрокаГодности = ИнтеграцияВетисAPIКлиентСервер.ПредставлениеДатыПоУмолчанию();
	Производитель              = НСтр("ru = 'Необходимо указать'");
	СтранаПроисхождения        = Справочники.СтраныМира.Россия;
	
	РеквизитыЗаполнения = Новый Структура;
	РеквизитыЗаполнения.Вставить("НекачественныйГруз");
	РеквизитыЗаполнения.Вставить("НомерПартии");
	РеквизитыЗаполнения.Вставить("СкоропортящаясяПродукция");
	РеквизитыЗаполнения.Вставить("СтранаПроисхождения");
	РеквизитыЗаполнения.Вставить("Производитель");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПредставлениеДатыВыработкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИнтеграцияВетисAPIКлиент.ОткрытьФормуЗаданияДат(ЭтотОбъект, ЭтотОбъект, Элемент, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеСрокаГодностиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИнтеграцияВетисAPIКлиент.ОткрытьФормуЗаданияДат(ЭтотОбъект, ЭтотОбъект, Элемент, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводительНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТолькоПросмотр"     , Ложь);
	ПараметрыФормы.Вставить("АдресХранения"      , АдресХраненияТаблицыПроизводителей());
	ПараметрыФормы.Вставить("СтранаПроисхождения", СтранаПроисхождения);
	
	Описание = Новый ОписаниеОповещения("ПроизводительВыборЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ФормаПодбораПроизводителей", ПараметрыФормы, ЭтотОбъект,,,, Описание, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводительВыборЗавершение(ВыбранныеПроизводители, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеПроизводители = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивПредприятий = Новый Массив;
	Для Каждого ТекПроизводитель Из ВыбранныеПроизводители Цикл
		НоваяСтрока = ТаблицаПроизводителей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекПроизводитель);
		МассивПредприятий.Добавить(ТекПроизводитель.Производитель);
	КонецЦикла;
	
	Производитель = СтрСоединить(МассивПредприятий,";");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	СтруктураДанных = Новый Структура;
	Для Каждого ТекРеквизит Из РеквизитыЗаполнения Цикл
		Если УстанавливатьНеЗаполненыеЗначения Или ЗначениеЗаполнено(ЭтотОбъект[ТекРеквизит.Ключ]) Тогда
			
			Если (ТекРеквизит.Ключ = "СкоропортящаясяПродукция" Или ТекРеквизит.Ключ = "НекачественныйГруз")
				И Не ЗначениеЗаполнено(ЭтотОбъект[ТекРеквизит.Ключ]) Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураДанных.Вставить(ТекРеквизит.Ключ, ЭтотОбъект[ТекРеквизит.Ключ]);
			
		КонецЕсли;
	КонецЦикла;
	
	Если УстанавливатьНеЗаполненыеЗначения Или ЗначениеЗаполнено(ФорматДатыВыработки) Тогда
		ДанныеПоДатамВыработки = Новый Структура;
		ДанныеПоДатамВыработки.Вставить("ФорматДатыВыработки"       , ФорматДатыВыработки);
		ДанныеПоДатамВыработки.Вставить("ДатаВыработкиНачало"       , ДатаВыработкиНачало);
		ДанныеПоДатамВыработки.Вставить("ДатаВыработкиОкончание"    , ДатаВыработкиОкончание);
		СтруктураДанных.Вставить("ДанныеПоДатамВыработки", ДанныеПоДатамВыработки);
	КонецЕсли;
	
	Если УстанавливатьНеЗаполненыеЗначения Или ЗначениеЗаполнено(ФорматСрокаГодности) Тогда
		ДанныеПоСрокамГодности = Новый Структура;
		ДанныеПоСрокамГодности.Вставить("ФорматСрокаГодности"                , ФорматСрокаГодности);
		ДанныеПоСрокамГодности.Вставить("ДатаОкончанияСрокаГодностиНачало"   , ДатаОкончанияСрокаГодностиНачало);
		ДанныеПоСрокамГодности.Вставить("ДатаОкончанияСрокаГодностиОкончание", ДатаОкончанияСрокаГодностиОкончание);
		СтруктураДанных.Вставить("ДанныеПоСрокамГодности", ДанныеПоСрокамГодности);
	КонецЕсли;
	
	Если УстанавливатьНеЗаполненыеЗначения Или ТаблицаПроизводителей.Количество() > 0 Тогда
		СтруктураДанных.Вставить("ТаблицаПроизводителей", ТаблицаПроизводителей);
	КонецЕсли;
	
	Закрыть(СтруктураДанных);

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция АдресХраненияТаблицыПроизводителей()
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаПроизводителей.Выгрузить(), Новый УникальныйИдентификатор());
	
КонецФункции

#КонецОбласти