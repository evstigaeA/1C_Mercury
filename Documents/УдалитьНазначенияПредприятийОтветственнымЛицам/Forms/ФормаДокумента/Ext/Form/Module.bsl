
#Область ОбработчикиСобытийФорм

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Свойство("Пользователь") Тогда
		Объект.Пользователь = Параметры.Пользователь;
	КонецЕсли;
	
	Если Параметры.Свойство("Организация") Тогда
		Объект.Организация = Параметры.Организация;
	Иначе
		Объект.Организация = ?(ЗначениеЗаполнено(Объект.Организация), Объект.Организация, ПараметрыСеанса.ТекущийПользователь.ОсновнаяОрганизация);
	КонецЕсли;
	
	Если Параметры.Свойство("Контрагент") Тогда
		Объект.Контрагент = Параметры.Контрагент;
	Иначе
		Объект.Контрагент = ОбщегоНазначенияУВССервер.КонтрагентПоОрганизации(Объект.Организация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Для Каждого ТекЗначение Из ВыбранноеЗначение Цикл
		
		НайденныеСтроки = Объект.Предприятия.НайтиСтроки(Новый Структура("Предприятие", ТекЗначение));
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока = Объект.Предприятия.Добавить();
			НоваяСтрока.Предприятие = ТекЗначение;
			НоваяСтрока.Операция    = ПредопределенноеЗначение("Перечисление.ВидыОперацииНазначенияПредприятий.Назначение");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий_Органиазация

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Объект.Предприятия.Очистить();
	Объект.Контрагент = ОбщегоНазначенияУВСВызовСервера.КонтрагентПоОрганизации(Объект.Организация);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий_Предприятие

&НаКлиенте
Процедура ПредприятияПредприятиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияУВСКлиент.ПредприятиеНачалоВыбора(Элемент, СтандартнаяОбработка,, Объект.Контрагент, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредприятияПредприятиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ОбщегоНазначенияУВСКлиент.ПредприятиеАвтоПодбор(Текст, ДанныеВыбора, СтандартнаяОбработка,, Объект.Контрагент, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредприятияПредприятиеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ОбщегоНазначенияУВСКлиент.ПредприятиеОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка,, Объект.Контрагент, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подбор_ТаблицаПредприятий(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОтборПоПредприятиям"           , ОбщегоНазначенияУВСВызовСервера.ПредприятияПоКонтрагенту(Объект.Контрагент));
	ПараметрыФормы.Вставить("ВозможностьВыбораБезУчетаСвязи", Ложь);
	ПараметрыФормы.Вставить("МножественныйВыбор"            , Истина);
	ПараметрыФормы.Вставить("РежимВыбора"                   , Истина);
	
	ОткрытьФорму("Справочник.Предприятия.ФормаВыбора", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти