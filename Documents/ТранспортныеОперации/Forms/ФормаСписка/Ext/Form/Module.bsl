#Область ОбработчикиСобытийФорм

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ИзменениеСтатусаЗаявкиНаОформлениеОперации" Тогда
		ОбновитьСписок();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("ОбновитьСписок", 90);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСписок()
	Элементы.Список.Обновить();
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "Список.Дата", Элементы.Дата.Имя);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДинамическийСписок(УстановитьОтбор)
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДокументТранспортныеОперации.Ссылка КАК Ссылка,
		|	ДокументТранспортныеОперации.ПометкаУдаления КАК ПометкаУдаления,
		|	ДокументТранспортныеОперации.Номер КАК Номер,
		|	ДокументТранспортныеОперации.Дата КАК Дата,
		|	ДокументТранспортныеОперации.Проведен КАК Проведен,
		|	ДокументТранспортныеОперации.ДатаТТН КАК ДатаТТН,
		|	ДокументТранспортныеОперации.НомерТТН КАК НомерТТН,
		|	ДокументТранспортныеОперации.Посредник КАК Посредник,
		|	ДокументТранспортныеОперации.ПредприятиеОтправитель КАК ПредприятиеОтправитель,
		|	ДокументТранспортныеОперации.ПредприятиеПолучатель КАК ПредприятиеПолучатель,
		|	ДокументТранспортныеОперации.СерияТТН КАК СерияТТН,
		|	ДокументТранспортныеОперации.СпособХраненияПриПеревозке КАК СпособХраненияПриПеревозке,
		|	ДокументТранспортныеОперации.ТипТТН КАК ТипТТН,
		|	ДокументТранспортныеОперации.Транспорт КАК Транспорт,
		|	ДокументТранспортныеОперации.МоментВремени КАК МоментВремени,
		|	ДокументТранспортныеОперации.КонтрагентОтправитель КАК КонтрагентОтправитель,
		|	ДокументТранспортныеОперации.КонтрагентПолучатель КАК КонтрагентПолучатель,
		|	ДокументТранспортныеОперации.Организация КАК Организация,
		|	ДокументТранспортныеОперации.Комментарий КАК Комментарий,
		|	ТаблицаСтатусДокумента.Статус КАК Статус
		|ИЗ
		|	Документ.ТранспортныеОперации КАК ДокументТранспортныеОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналРегистрацииСостоянийЗаявокНаОформлениеОпераций.СрезПоследних(, ДокументСсылка ССЫЛКА Документ.ТранспортныеОперации) КАК ТаблицаСтатусДокумента
		|		ПО (ТаблицаСтатусДокумента.ДокументСсылка = ДокументТранспортныеОперации.Ссылка)";
		
	Если УстановитьОтбор Тогда
	
		ТекстЗапроса = ТекстЗапроса + "
			|ГДЕ
			|	(ДокументТранспортныеОперации.Организация, ДокументТранспортныеОперации.КонтрагентПолучатель) В
			|		(ВЫБРАТЬ
			|				УполномоченноеГашение.Организация КАК Организация,
			|				УполномоченноеГашение.Контрагент КАК Контрагент
			|			ИЗ
			|				РегистрСведений.УполномоченноеГашение КАК УполномоченноеГашение)
			|	И ТаблицаСтатусДокумента.Статус = ЗНАЧЕНИЕ(Справочник.СтатусыЗаявок.УспешноОбработана)";
		
	КонецЕсли;
	
	СвойстваСписка.ОсновнаяТаблица  = "Документ.ТранспортныеОперации";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
КонецПроцедуры

&НаКлиенте
Процедура УполномоченноеГашение(Команда)
	
	Элементы.ФормаУполномоченноеГашение.Пометка = Не Элементы.ФормаУполномоченноеГашение.Пометка;
	
	НастроитьДинамическийСписок(Элементы.ФормаУполномоченноеГашение.Пометка);
	
КонецПроцедуры

#КонецОбласти